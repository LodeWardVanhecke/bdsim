  TITLE, "e- Beam Switchyard (ILC2005)"

  OPTION, -INTER, -ECHO, VERIFY
  ASSIGN, PRINT="ebsy.print"
  ASSIGN, ECHO="ebsy.echo"

! ==============================================================================
! Common parameters and definitions
! ------------------------------------------------------------------------------

  Cb : CONSTANT=1.0E10/CLIGHT

  Ef     := 250.0
  Brho   := Cb*Ef
  Efact  := Ef/500
  L12MM  := 0.012
  L50CM  := 0.5
  L1M    := 1.0
  L2M    := 2.0
  LFBC   := 0.2
  LFBK   := 0.3
  Lspace := 0.1

  QBDS1  : QUAD, TYPE="QBDS1", L=L50CM/2, APER=L12MM/2
  QFACT1 := 1/(QBDS1[APER]*Brho)
  QBDS2  : QUAD, TYPE="QBDS2", L=L1M/2,   APER=L12MM/2
  QFACT2 := 1/(QBDS2[APER]*Brho)
  QBDS3  : QUAD, TYPE="QBDS3", L=L2M/2, APER=L12MM/2
  QFACT3 := 1/(QBDS3[APER]*Brho)

  BPMQ079  : MONI
  MMOVER   : MARK
  FBCXY    : INST
  FBKXY    : INST
  BPMMB079 : MONI
  WS       : WIRE
  BPMWS    : MONI

! ==============================================================================
! Load beamline definitions
! ------------------------------------------------------------------------------

  CALL, FILENAME="ebsy.xsif"

! ==============================================================================
! Input beam definitions
! ------------------------------------------------------------------------------

  TBETX := 50.525915605603
  TALFX := -2.379589233378
  TBETY :=  9.563928674469
  TALFY :=  0.510997181234

  TWSS0 : BETA0, ENERGY=Ef, BETX=TBETX, ALFX=TALFX, BETY=TBETY, ALFY=TALFY

  BEAM, PARTICLE=ELECTRON, ENERGY=Ef

! ==============================================================================
! SUBROUTINEs
! ------------------------------------------------------------------------------

  MC90F : SUBROUTINE

  ! match 90/90 FODO cell phase advances

    USE, C90F
    CELL
      VARY, BQF90, STEP=1.E-5, LOWER=0, UPPER=+9.6*Efact
      CONSTR, #E, MUX=90/360
      LMDIF
      MIGRAD
    ENDMATCH
    VALUE, BQF90/Efact
  ENDSUBROUTINE

! ------------------------------------------------------------------------------

  MC180 : SUBROUTINE

  ! match 180/90 FODO cell phase advances

    USE, C180
    MATCH, LINE=C90D
      VARY, BQF180, STEP=1.E-5, LOWER=0, UPPER=+9.6*Efact
      VARY, BQD180, STEP=1.E-5, UPPER=0, LOWER=-9.6*Efact
     !VARY, L180A,  STEP=1.E-5
     !VARY, L180B,  STEP=1.E-5
      CONSTR, #E, LINE=C90D, MUX=180/360, MUY=90/360
      LMDIF
      MIGRAD, TOL=1E-20
    ENDMATCH
    VALUE, BQF180/Efact
    VALUE, BQD180/Efact
   !VALUE, L180A
   !VALUE, L180B
  ENDSUBROUTINE

! ------------------------------------------------------------------------------

  MC45 : SUBROUTINE

  ! match 45/45 FODO cell phase advances

    USE, C45
    CELL
      VARY, BQD45, STEP=1.E-5, LOWER=0, UPPER=+9.6*Efact
      CONSTR, #E, MUY=45/360
      LMDIF
      MIGRAD
    ENDMATCH
    VALUE, BQD45/Efact
  ENDSUBROUTINE

! ------------------------------------------------------------------------------

  MC90X : SUBROUTINE

  ! match 90/90 SBD FODO cell phase advances

    USE, C90X
    CELL
      VARY, BQF90X, STEP=1.E-5 !, LOWER=0, UPPER=+9.6*Efact
      VARY, BQD90X, STEP=1.E-5 !, LOWER=0, UPPER=+9.6*Efact
      CONSTR, #E, MUX=90/360, MUY=90/360
      LMDIF
      MIGRAD
    ENDMATCH
    VALUE, BQF90X/Efact
    VALUE, BQD90X/Efact
  ENDSUBROUTINE

! ------------------------------------------------------------------------------

  MM9045 : SUBROUTINE

  ! match 90/90 FODO cell to 45/45 FODO cell

    USE, M9045
    MATCH, LINE=C90F
      VARY, BQD9045, STEP=1.E-5, UPPER=0, LOWER=-9.6*Efact
      VARY, BQF9045, STEP=1.E-5, LOWER=0, UPPER=+9.6*Efact
      VARY, L9045A,  STEP=1.E-5
      VARY, L9045B,  STEP=1.E-5
      CONSTR, #E, LINE=C45
      LMDIF
      MIGRAD
    ENDMATCH
    VALUE, BQD9045/Efact
    VALUE, BQF9045/Efact
    VALUE, L9045A
    VALUE, L9045B
  ENDSUBROUTINE

! ------------------------------------------------------------------------------

  MWS45 : SUBROUTINE

  ! tweak WS1/WS2 seperation to get close to 45/45

    USE, SKSRC1
    MATCH, LINE=C90F
      VARY, LWS45, STEP=1.E-5
      WEIGHT, MUX=1, BETX=0, ALFX=0, DX=0, DPX=0, &
              MUY=2, BETY=0, ALFY=0, DY=0, DPY=0
      COUPLE, WS[1]/WS[2], MUX=45/360, MUY=45/360
      LMDIF
      MIGRAD
    ENDMATCH
    VALUE, LWS45
  ENDSUBROUTINE

! ------------------------------------------------------------------------------

  MM4590X : SUBROUTINE

  ! match 45/45 FODO cell to 90/90 extraction FODO cell

    USE, M4590X
    MATCH, LINE=C45
      VARY, BQF4590, STEP=1.E-5, LOWER=0, UPPER=+9.6*Efact
      VARY, BQD4590, STEP=1.E-5, UPPER=0, LOWER=-9.6*Efact
      VARY, L4590A,  STEP=1.E-5, LOWER=0.5
      VARY, L4590B,  STEP=1.E-5, LOWER=0.5
      CONSTR, #E, LINE=C90X
      LMDIF, TOL=1.E-20
      MIGRAD, TOL=1.E-20
    ENDMATCH
    VALUE, BQF4590/Efact
    VALUE, BQD4590/Efact
    VALUE, L4590A
    VALUE, L4590B
  ENDSUBROUTINE

! ------------------------------------------------------------------------------

  MTWSS0 : SUBROUTINE

  ! find matched input Twiss

    USE, L2SS
    MATCH, BETA0=TWSS0
      VARY, TBETX, STEP=1.E-5, LOWER=0.1
      VARY, TALFX, STEP=1.E-5
      VARY, TBETY, STEP=1.E-5, LOWER=0.1
      VARY, TALFY, STEP=1.E-5
      CONSTR, #E, LINE=C90F
      LMDIF
      MIGRAD
    ENDMATCH
    VALUE, TBETX
    VALUE, TALFX
    VALUE, TBETY
    VALUE, TALFY

  ! plot results

    USE, EBSY
    PRINT, FULL
    TWISS, SAVE, BETA0=TWSS0
    PLOT, TABLE=TWISS, HAXIS=S, VAXIS=BETX,BETY, STYLE=100, SPLINE=.T.
    PLOT, TABLE=TWISS, HAXIS=S, VAXIS=WX,WY, STYLE=100, SPLINE=.F.
  ENDSUBROUTINE

! ==============================================================================
! COMMANDs
! ------------------------------------------------------------------------------

  SETPLOT, XSIZE=25.4, YSIZE=20.32
  OPTION, ECHO

! do fitting

 !MC90F
 !MC180
 !MC45
 !MC90X
 !MM9045
 !MWS45
 !MM4590X
 !MTWSS0

! generate output files and plots

  USE, EBSY
  PRINT, FULL
  SELECT, OPTICS, FULL
  SAVEBETA, TWSSX, EXT0
  SAVEBETA, TREATY, #E
  TWISS, BETA0=TWSS0, SAVE, TAPE="ebsy_twiss.tape"
  SURVEY, TAPE="ebsy_survey.tape"

  PLOT, TABLE=TWISS, HAXIS=S, VAXIS=BETX,BETY, &
    STYLE=100, SPLINE=.T., FILE="ebsy"

! display twiss for input to next area

  VALUE, TREATY[ENERGY]
  VALUE, TREATY[BETX],TREATY[ALFX]
  VALUE, TREATY[BETY],TREATY[ALFY]

  VALUE, TWSSX[ENERGY]
  VALUE, TWSSX[BETX],TWSSX[ALFX]
  VALUE, TWSSX[BETY],TWSSX[ALFY]

  STOP
