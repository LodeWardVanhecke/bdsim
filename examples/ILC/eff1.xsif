! ==============================================================================
! area=eff1 (ILCFF9)
! ------------------------------------------------------------------------------
! 16-MAR-2005, M. Woodley
!    ILC2005 configuration: from Andrei Seryi's ILCFF8.mad; reduce BS1 bends
!    for zero net bending; rematch sextupoles; add energy spectrometer chicane;
!    retune for bandwidth
! 07-SEP-2004, M. Woodley
!    NLC2004 configuration
! 30-jul-2003, PT:
!    move QF5 through QD2A quad strengths to the configuration files.
! 24-jul-2003, PT:
!    new preamble.  Move bend angles, sext strengths, octupole strengths,
!    decapole strengths, and some definitions around the IR to the 
!    configuration ("patch") files.
! 23-JUL-2003, PT:
!    add BPMVIRT to octupoles, decapoles, any sextupoles w/o a BPM.
! 20-MAY-2003, M. Woodley
!    Add photon dump (fixed 1 cm radius circular aperture) per L. Keller
! 04-APR-2003, M. Woodley
!    NLC2003 configuration:
!
!    - full length Raimondi/Seryi compact final focus system
!    - "short" superconducting final doublet optimized for 250 GeV beam
!    - bend strengths optimized for 250 GeV beam
!    - "large" detector with 5 T field;
!
!    give spoilers and absorbers non-zero physical lengths and set gaps per
!    L. Keller note of 20-DEC-2002; move crab cavity upstream of ST4; increase
!    bore of matching quadrupoles QM11-16 from 2 cm to 4 cm; add PPS stoppers;
!    add muon spoilers
! 24-JUL-2002, M. Woodley
!    Set element TYPE attributes for GEANT translation via MAD OPTICS output
!    per G. Blair
! 01-APR-2002, M. Woodley
!    From nlc130.effh.xsif; split OC0 in half; per A. Seryi: change aperture
!    radius of QD2B/QF3/QD2A from 1 cm to 3 cm; increase strength of OC7
!    octupoles (A-E) by a factor of 1.3; change aperture of B5/QD8/QF7/QD6/QF5/
!    SF5/QD4/SF4 from 1 cm to 1.25 cm
! 03-DEC-2001, A. Seryi
!    Switch to "short" final doublet ... create nlc130.effh.xsif for TRC
!    studies 
! 14-NOV-2001, A. Seryi
!    Bend strengths optimized for 250 GeV beam
! 20-SEP-2001, P. Tenenbaum
!    Change OC7 aperture to 7 mm, final doublet quads/sexts to 1 cm;
!    eliminated unsplit beamline and elements; added SPE energy spoiler to
!    shadow energy absorber; added assorted instrumentation and controls,
!    stoppers, SBD pulsed bends, etc.
! 28-AUG-2001, M. Woodley
!    BNLC500_FF112 from P. Raimondi
! ------------------------------------------------------------------------------

! ==============================================================================
! dipoles
! ------------------------------------------------------------------------------

! energy collimation / soft bend

  LB12 := 12.0

  GB1    := 0.01
  GB1p25 := 0.0125
  GB3    := 0.03

  TBS1 := Bsign*(-0.8394601319E-4)
  TB5  := Bsign*( 0.1213152148E-3)
  TB2  := Bsign*( 0.991473613E-4 )
  TB1  := Bsign*( 0.357678789E-4 )
  
  BS1 : SBEN, L=LB12/2, ANGLE=TBS1/2, HGAP=GB1,    FINT=0, FINTX=0, TYPE="H20"
  B5  : SBEN, L=LB12/2, ANGLE=TB5/2,  HGAP=GB1p25, FINT=0, FINTX=0, TYPE="H25"
  B2  : SBEN, L=LB12/2, ANGLE=TB2/2,  HGAP=GB3,    FINT=0, FINTX=0, TYPE="H60"
  B1  : SBEN, L=LB12/2, ANGLE=TB1/2,  HGAP=GB3,    FINT=0, FINTX=0, TYPE="H60"

! energy spectrometer

  ECETA := 5.0               !peak dispersion (mm)
  ECRMM := 0.485436893589E-4 !d(ANGLE)/d(ETA) (rad/mm)
  ECANG := ECETA*ECRMM       !chicane bend angle (rad)

  ZBEC1  := 3.0                   !pole "Z" length (m)
  RBEC1  := ZBEC1/SIN(ECANG)      !bend radius (m)
  LBEC1  := RBEC1*ECANG           !effective length (m)
  ABEC1a := ASIN(0.5*ZBEC1/RBEC1) !bend angle ("short" half)
  LBEC1a := RBEC1*ABEC1a          !effective length ("short" half)
  ABEC1b := ECANG-ABEC1a          !bend angle ("long" half)
  LBEC1b := LBEC1-LBEC1a          !effective length ("long" half)

  BEC1a : SBEN, L=LBEC1a, ANGLE=+ABEC1a, HGAP=GB1, TYPE="H20", &
                E1=0,      FINT=0.5, FINTX=0
  BEC1b : SBEN, L=LBEC1b, ANGLE=+ABEC1b, HGAP=GB1, TYPE="H20", &
                E2=+ECANG, FINT=0,   FINTX=0.5
  BEC4a : SBEN, L=LBEC1b, ANGLE=+ABEC1b, HGAP=GB1, TYPE="H20", &
                E1=+ECANG, FINT=0.5, FINTX=0
  BEC4b : SBEN, L=LBEC1a, ANGLE=+ABEC1a, HGAP=GB1, TYPE="H20", &
                E2=0,      FINT=0,   FINTX=0.5

  ZBEC2  := 6.0                   !pole "Z" length (m)
  RBEC2  := ZBEC2/SIN(ECANG)      !bend radius (m)
  LBEC2  := RBEC2*ECANG           !effective length (m)
  ABEC2a := ASIN(0.5*ZBEC2/RBEC2) !bend angle ("short" half)
  LBEC2a := RBEC2*ABEC2a          !effective length ("short" half)
  ABEC2b := ECANG-ABEC2a          !bend angle ("long" half)
  LBEC2b := LBEC2-LBEC2a          !effective length ("long" half)

  BEC2a : SBEN, L=LBEC2b, ANGLE=-ABEC2b, HGAP=GB1, TYPE="H20", &
                E1=-ECANG, FINT=0.5, FINTX=0
  BEC2b : SBEN, L=LBEC2a, ANGLE=-ABEC2a, HGAP=GB1, TYPE="H20", &
                E2=0,      FINT=0,   FINTX=0.5
  BEC3a : SBEN, L=LBEC2a, ANGLE=-ABEC2a, HGAP=GB1, TYPE="H20", &
                E1=0,      FINT=0.5, FINTX=0
  BEC3b : SBEN, L=LBEC2b, ANGLE=-ABEC2b, HGAP=GB1, TYPE="H20", &
                E2=-ECANG, FINT=0,   FINTX=0.5

! bend-to-bend path lengths

  ZECBBo := 16.1              !outer bend-to-bend "Z" spacing (m)
  LECBBo := ZECBBo/COS(ECANG) !outer bend-to-bend path length (m)
  LECBBi := 1.4               !inner bend-to-bend "Z" spacing (m)

  DECBBo : DRIF, L=LECBBo
  DECBBi : DRIF, L=LECBBi/2

! single beam dumper: uses a pair of PBH-type pulsed bends, 9 kG each, 3 m long
! each, to give a kick angle of 3.24 mrad; this one sends the beam to a 120 Hz,
! 1 bunch/train dump (basically the SLC main dumps fit this bill)

 !SBD_FF : HKIC, L=3.0/2, TYPE="H25"
  SBD_FF : DRIF, L=3.0/2, TYPE="H25" !this is just a placeholder

! ==============================================================================
! quadrupoles
! ------------------------------------------------------------------------------

! collimation / final focus

  LQ2  := 2.0
  LQF1 := 2.0
  LQD0 := 2.2

  rQ1    := 0.01
  rQ1p25 := 0.0125
  rQ2    := 0.02
  rQ3    := 0.03

  KQB3   := -0.024980261682
  KQB2   :=  0.982705007366E-3
  KQB1   :=  0.022717941178
  KQB0   := -0.021939156638
  KQB0M  :=  KQB0
  KQB1M  :=  KQB1
  KQB2M  := -0.122420786593E-2
  KQB3M  := -0.030076318908
  KQS1   :=  0.01097678
  KQS2   := -0.01201029
  KQS3   :=  0.01195141
  KQM16  := -0.932086368071E-2
  KQM15  :=  0.499235704254E-2
  KQM14  := -0.306167094467E-2
  KQM13a := -0.616285306201E-2
  KQM13b :=  0.042946171654
  KQM12  := -0.018120377666
  KQM11  :=  0.016996304506
  KQD10  := -0.914613E-2
  KQF9   :=  0.01112554
  KQD8   := -0.01596707
  KQF7   :=  0.02085692
  KQD6   := -0.01596707
  KQF5   :=  0.0109844
  KQD4   := -0.8664E-2
  KQD2B  := -0.20227E-2
  KQF3   :=  0.01252071
  KQD2A  := -0.01126068
  KQF1   :=  0.09626012
  KQD0   := -0.16982715

  QB3   : QUAD, L=LQ2/2,  K1=KQB3,   APER=rQ1,    TYPE="D20"
  QB2   : QUAD, L=LQ2/2,  K1=KQB2,   APER=rQ1,    TYPE="D20"
  QB1   : QUAD, L=LQ2/2,  K1=KQB1,   APER=rQ1,    TYPE="D20"
  QB0   : QUAD, L=LQ2/2,  K1=KQB0,   APER=rQ1,    TYPE="D20"
  QB0M  : QUAD, L=LQ2/2,  K1=KQB0M,  APER=rQ1,    TYPE="D20"
  QB1M  : QUAD, L=LQ2/2,  K1=KQB1M,  APER=rQ1,    TYPE="D20"
  QB2M  : QUAD, L=LQ2/2,  K1=KQB2M,  APER=rQ1,    TYPE="D20"
  QB3M  : QUAD, L=LQ2/2,  K1=KQB3M,  APER=rQ1,    TYPE="D20"
  QS1   : QUAD, L=LQ2/2,  K1=KQS1,   APER=rQ1,    TYPE="D20"
  QS2   : QUAD, L=LQ2/2,  K1=KQS2,   APER=rQ1,    TYPE="D20"
  QS3   : QUAD, L=LQ2/2,  K1=KQS3,   APER=rQ1,    TYPE="D20"
  QM16  : QUAD, L=LQ2/2,  K1=KQM16,  APER=rQ2,    TYPE="D40"
  QM15  : QUAD, L=LQ2/2,  K1=KQM15,  APER=rQ2,    TYPE="D40"
  QM14  : QUAD, L=LQ2/2,  K1=KQM14,  APER=rQ2,    TYPE="D40"
  QM13a : QUAD, L=LQ2/2,  K1=KQM13a, APER=rQ2,    TYPE="D40"
  QM13b : QUAD, L=LQ2/2,  K1=KQM13b, APER=rQ2,    TYPE="D40"
  QM12  : QUAD, L=LQ2/2,  K1=KQM12,  APER=rQ2,    TYPE="D40"
  QM11  : QUAD, L=LQ2/2,  K1=KQM11,  APER=rQ2,    TYPE="D40"
  QD10  : QUAD, L=LQ2/2,  K1=KQD10,  APER=rQ1,    TYPE="D20"
  QF9   : QUAD, L=LQ2/2,  K1=KQF9,   APER=rQ1,    TYPE="D20"
  QD8   : QUAD, L=LQ2/2,  K1=KQD8,   APER=rQ1p25, TYPE="D25"
  QF7   : QUAD, L=LQ2/2,  K1=KQF7,   APER=rQ1p25, TYPE="D25"
  QD6   : QUAD, L=LQ2/2,  K1=KQD6,   APER=rQ1p25, TYPE="D25"
  QF5   : QUAD, L=LQ2/2,  K1=KQF5,   APER=rQ1p25, TYPE="D25"
  QD4   : QUAD, L=LQ2/2,  K1=KQD4,   APER=rQ1p25, TYPE="D25"
  QD2B  : QUAD, L=LQ2/2,  K1=KQD2B,  APER=rQ3,    TYPE="D60"
  QF3   : QUAD, L=LQ2/2,  K1=KQF3,   APER=rQ3,    TYPE="D60"
  QD2A  : QUAD, L=LQ2/2,  K1=KQD2A,  APER=rQ3,    TYPE="D60"
  QF1   : QUAD, L=LQF1/2, K1=KQF1,   APER=rQ1,    TYPE="D20"
  QD0   : QUAD, L=LQD0/2, K1=KQD0,   APER=rQ1,    TYPE="D20"

! IR skew quad

 !SQ3FF : QUAD, L=0.3/2, K1=0, APER=rQ1, TILT, TYPE="D20"
  SQ3FF : DRIF, L=0.3/2 !this is just a placeholder

! ==============================================================================
! sextupoles
! ------------------------------------------------------------------------------

  LS1  := 1.0
  LSF1 := 0.3
  LSD0 := 0.6

  rS1    := 0.01
  rS1p25 := 0.0125

  KSF6 := Bsign*(-1.69939443454 )
  KSF5 := Bsign*( 0.419515957698)
  KSD4 := Bsign*(-3.301438410805)
  KSF1 := Bsign*( 7.252873476491)
  KSD0 := Bsign*(-7.740702674458)

  SF6 : SEXT, L=LS1/2,  K2=KSF6, APER=rS1,    TYPE="D20"
  SF5 : SEXT, L=LS1/2,  K2=KSF5, APER=rS1p25, TYPE="D25"
  SD4 : SEXT, L=LS1/2,  K2=KSD4, APER=rS1p25, TYPE="D25"
  SF1 : SEXT, L=LSF1/2, K2=KSF1, APER=rS1,    TYPE="D20"
  SD0 : SEXT, L=LSD0/2, K2=KSD0, APER=rS1,    TYPE="D20"

! ==============================================================================
! octupoles
! ------------------------------------------------------------------------------

  LO3  := 3.0
  LO2  := 2.0
  LO1  := 1.0
  LOp3 := 0.3
  LOp1 := 0.1

  rO1  := 0.01
  rOp7 := 0.007

  KOC7E := -4.0753E3
  KOC7D :=  3.3727E3
  KOC7C := -3.8645E3
  KOC7B :=  4.0261E3
  KOC7A := -1.7566E3
  KOC10 :=  5.91716
  KOC1  :=-32.54437
  KOC0  :=  3.0769222E2

  OC7E : OCTU, L=LO3/2,  K3=KOC7E, APER=rOp7, TYPE="D14"
  OC7D : OCTU, L=LO3/2,  K3=KOC7D, APER=rOp7, TYPE="D14"
  OC7C : OCTU, L=LO2/2,  K3=KOC7C, APER=rOp7, TYPE="D14"
  OC7B : OCTU, L=LO2/2,  K3=KOC7B, APER=rOp7, TYPE="D14"
  OC7A : OCTU, L=LOp3/2, K3=KOC7A, APER=rOp7, TYPE="D14"
  OC10 : OCTU, L=LO1/2,  K3=KOC10, APER=rOp7, TYPE="D14"
  OC4  : DRIF, L=LOp1
  OC1  : OCTU, L=LOp3/2, K3=KOC1,  APER=rO1,  TYPE="D20"
  OC0  : OCTU, L=LOp1/2, K3=KOC0,  APER=rO1,  TYPE="D20"

! ==============================================================================
! decapoles
! ------------------------------------------------------------------------------

  VDEC46 := -1.77514E5

  KDEC6L := Bsign*VDEC46/2
  KDEC4L := Bsign*VDEC46/2

  DEC6 : MULTIPOLE, LRAD=1.0E-4, K4L=KDEC6L, TYPE="D25"
  DEC4 : MULTIPOLE, LRAD=1.0E-4, K4L=KDEC4L, TYPE="D25"

! ==============================================================================
! spoilers and absorbers
! ------------------------------------------------------------------------------
! NOTE: spoiler and absorber apertures have not been optimized for this
!        system (!) ... these are old NLC values
! ------------------------------------------------------------------------------
! NOTE: spoilers (SP*) and absorbers (AB*) are movable 4-jaw devices;
!       protection collimators (PC*) are fixed circular apertures;
!       photon masks (MSK*) are fixed vertical slits;
!       SP1-5 are 0.6 radiation lengths copper;
!       SPEX is 1.0 radiation length titanium;
!       AB2-5 are 30.0 radiation lengths copper;
!       ABE, AB7-10, and MSK1-2 are 30.0 radiation lengths tungsten;
!       PC1-11 are 15.0 radiation lengths copper;
!       special element TYPE attributes for GEANT translation via MAD OPTICS
!       output per G. Blair
! ------------------------------------------------------------------------------

! per L. Keller for tail-folding octupole doublets OFF

  XGAPSP1  :=  0.300e-3 ; YGAPSP1  :=  0.250e-3
  XGAPAB2  :=  4.000e-3 ; YGAPAB2  :=  4.000e-3
  XGAPSP2  :=  0.300e-3 ; YGAPSP2  :=  0.200e-3
  RPC1     :=  5.000e-3
  XGAPAB3  :=  4.000e-3 ; YGAPAB3  :=  4.000e-3
  XGAPSP3  :=  0.300e-3 ; YGAPSP3  :=  0.250e-3
  RPC2     :=  5.000e-3
  RPC3     :=  5.000e-3
  XGAPAB4  :=  4.000e-3 ; YGAPAB4  :=  4.000e-3
  XGAPSP4  :=  0.300e-3 ; YGAPSP4  :=  0.200e-3
  RPC4     :=  5.000e-3
  RPC5     :=  5.000e-3
  XGAPAB5  :=  4.000e-3 ; YGAPAB5  :=  4.000e-3
  XGAPSP5  :=  0.420e-3 ; YGAPSP5  :=  0.250e-3
  RPC6     :=  5.000e-3
  RPDUMP   := 10.000e-3
  RPC7     :=  5.000e-3
  XGAPSPEX :=  3.200e-3
  RPC8     :=  5.000e-3
  RPC9     :=  5.000e-3
  RPC10    :=  5.000e-3
  XGAPABE  :=  1.000e-3 ; YGAPABE  :=  1.000e-3
  RPC11    :=  5.000e-3
  XGAPAB10 :=  4.400e-3 ; YGAPAB10 :=  4.400e-3
  XGAPAB9  :=  6.600e-3 ; YGAPAB9  :=  3.000e-3
  XGAPAB7  :=  4.500e-3 ; YGAPAB7  :=  3.000e-3
  XGAPMSK1 :=  8.300e-3
  XGAPMSK2 :=  8.500e-3

! per L. Keller for tail-folding octupole doublets ON

 !SET, XGAPSP1, 0.750e-3 ; SET, YGAPSP1, 0.750e-3
 !SET, XGAPSP2, 0.750e-3 ; SET, YGAPSP2, 0.750e-3
 !SET, XGAPSP3, 0.750e-3 ; SET, YGAPSP3, 0.750e-3
 !SET, XGAPSP4, 0.750e-3 ; SET, YGAPSP4, 0.750e-3
 !SET, XGAPSP5, 0.750e-3 ; SET, YGAPSP5, 0.750e-3

  LSPB  := 0.0086 ! 0.6 r.l. Cu
  LSPE  := 0.0356 ! 1.0 r.l. Ti
  LAB   := 0.4290 !30.0 r.l. Cu
  LABFF := 0.1050 !30.0 r.l. W
  LPC   := 0.2145 !15.0 r.l. Cu
  LMSK  := 0.1050 !30.0 r.l. W

  SP1   : RCOL, L=LSPB,  TYPE="X0.6Y0.5",  XSIZE=XGAPSP1,  YSIZE=YGAPSP1
  AB2   : RCOL, L=LAB,   TYPE="X8Y8",      XSIZE=XGAPAB2,  YSIZE=YGAPAB2
  SP2   : RCOL, L=LSPB,  TYPE="X0.6Y0.4",  XSIZE=XGAPSP2,  YSIZE=YGAPSP2
  PC1   : ECOL, L=LPC,   TYPE="X10Y10",    XSIZE=RPC1,     YSIZE=RPC1
  AB3   : RCOL, L=LAB,   TYPE="X8Y8",      XSIZE=XGAPAB3,  YSIZE=YGAPAB3
  SP3   : RCOL, L=LSPB,  TYPE="X0.6Y0.5",  XSIZE=XGAPSP3,  YSIZE=YGAPSP3
  PC2   : ECOL, L=LPC,   TYPE="X10Y10",    XSIZE=RPC2,     YSIZE=RPC2
  PC3   : ECOL, L=LPC,   TYPE="X10Y10",    XSIZE=RPC3,     YSIZE=RPC3
  AB4   : RCOL, L=LAB,   TYPE="X8Y8",      XSIZE=XGAPAB4,  YSIZE=YGAPAB4
  SP4   : RCOL, L=LSPB,  TYPE="X0.6Y0.4",  XSIZE=XGAPSP4,  YSIZE=YGAPSP4
  PC4   : ECOL, L=LPC,   TYPE="X10Y10",    XSIZE=RPC4,     YSIZE=RPC4
  PC5   : ECOL, L=LPC,   TYPE="X10Y10",    XSIZE=RPC5,     YSIZE=RPC5
  AB5   : RCOL, L=LAB,   TYPE="X8Y8",      XSIZE=XGAPAB5,  YSIZE=YGAPAB5
  SP5   : RCOL, L=LSPB,  TYPE="X0.84Y0.5", XSIZE=XGAPSP5,  YSIZE=YGAPSP5
  PC6   : ECOL, L=LPC,   TYPE="X10Y10",    XSIZE=RPC6,     YSIZE=RPC6
  PDUMP : ECOL, L=LPC,   TYPE="X20Y20",    XSIZE=RPDUMP,   YSIZE=RPDUMP
  PC7   : ECOL, L=LPC,   TYPE="X10Y10",    XSIZE=RPC7,     YSIZE=RPC7
  SPEX  : RCOL, L=LSPE,  TYPE="X6.4Y0",    XSIZE=XGAPSPEX
  PC8   : ECOL, L=LPC,   TYPE="X10Y10",    XSIZE=RPC8,     YSIZE=RPC8
  PC9   : ECOL, L=LPC,   TYPE="X10Y10",    XSIZE=RPC9,     YSIZE=RPC9
  PC10  : ECOL, L=LPC,   TYPE="X10Y10",    XSIZE=RPC10,    YSIZE=RPC10
  ABE   : RCOL, L=LABFF, TYPE="X2Y2",      XSIZE=XGAPABE,  YSIZE=YGAPABE
  PC11  : ECOL, L=LPC,   TYPE="X10Y10",    XSIZE=RPC11,    YSIZE=RPC11
  AB10  : RCOL, L=LABFF, TYPE="X8.8Y8.8",  XSIZE=XGAPAB10, YSIZE=YGAPAB10
  AB9   : RCOL, L=LABFF, TYPE="X13.2Y6",   XSIZE=XGAPAB9,  YSIZE=YGAPAB9
  AB7   : RCOL, L=LABFF, TYPE="X9Y6",      XSIZE=XGAPAB7,  YSIZE=YGAPAB7
  MSK1  : RCOL, L=LMSK,  TYPE="X16.6Y0",   XSIZE=XGAPMSK1
  MSK2  : RCOL, L=LMSK,  TYPE="X17Y0",     XSIZE=XGAPMSK2

! ==============================================================================
! drifts
! ------------------------------------------------------------------------------

! collimation / final focus

  LDBM  :=  2.1481
  LDB0  :=  2.1481
  dLDB0 := 25.752235097059
  LDB1  := 79.8744
  LDB2  :=  4.3039
  LDBMM :=  2.1481
  LDB1M := 35.25883770713
  LDB0M := 42.220804966667

  dLmdw := 0

  DBM  : DRIF, L= LDBM
  DB0  : DRIF, L= LDB0+dLDB0
  DB1  : DRIF, L= LDB1-dLDB0
  DB2  : DRIF, L= LDB2
  DBMM : DRIF, L= LDBMM
  DB1M : DRIF, L= LDB1M
  DB0M : DRIF, L= LDB0M
  LX0  : DRIF, L= 0.3
  DS3  : DRIF, L=12.0
  DSM  : DRIF, L= 1.0
  D12  : DRIF, L=10.797027991439+dLmdw
  D11  : DRIF, L=12.0
  D10  : DRIF, L=12.0
  D9   : DRIF, L=12.0
  D8   : DRIF, L=12.0
  D7E  : DRIF, L=10.7
  D7D  : DRIF, L= 4.0
  D7C  : DRIF, L=37.0
  D7B  : DRIF, L= 3.0
  D7A  : DRIF, L=37.0
  D7   : DRIF, L=12.0
  D710 : DRIF, L= 3.0
  D6A  : DRIF, L= 0.86777
  D6   : DRIF, L= 9.23999
  D5B  : DRIF, L= 6.89534-4*SBD_FF[L]-2*LX0[L]
  D5A  : DRIF, L= 6.89534
  D4   : DRIF, L= 9.23999
  D4A  : DRIF, L= 0.86777
  D3   : DRIF, L=20.01176
  D3B  : DRIF, L=28.6
  D2   : DRIF, L=77.08464
  D1C  : DRIF, L= 0.15
  D1B  : DRIF, L= 1.35
  D1A  : DRIF, L= 0.05
  D0   : DRIF, L= 3.51

  DBMa  : DRIF, L=DBM[L]-LSPB
  DBMb  : DRIF, L=DBM[L]-(LAB+LSPB)
  DB0a  : DRIF, L=DB0[L]-0.5
  DB0b  : DRIF, L=0.5-LPC
  DB1a  : DRIF, L=DB1[L]/2
  DB1b  : DRIF, L=DB1[L]-DB1a[L]
  DB1d  : DRIF, L=0.5-LPC
  DB1c  : DRIF, L=DB1[L]-(DB1a[L]+LPC+DB1d[L])
  DB2a  : DRIF, L=DB2[L]-0.5
  DB2b  : DRIF, L=0.5-LPC
  DBMMa : DRIF, L=DBMM[L]-(LAB+LSPB)
  DB1Ma : DRIF, L=DB1M[L]-0.5
  DB1Mb : DRIF, L=0.5-LPC
  DB0Mb : DRIF, L=3.0-(LAB+LSPB)
  DB0Ma : DRIF, L=DB0M[L]-3.0
  LX0a  : DRIF, L=LX0[L]-LPC
  DSMa  : DRIF, L=DSM[L]-LSPE
  DS3a  : DRIF, L=DS3[L]-0.5
  DS3b  : DRIF, L=0.5-LPC
  LX0b  : DRIF, L=LX0[L]-LABFF
  D710a : DRIF, L=D710[L]-LABFF
  D3c   : DRIF, L=24.89536
  D2b   : DRIF, L=34.825
  D2a   : DRIF, L=40.295
  DMSQ  : DRIF, L=1.45464
  MUSP1 : DRIF, L=9.0, TYPE="muon spoiler"
  D7Ea  : DRIF, L=(D7E[L]-MUSP1[L])/2
  MUSP2 : DRIF, L=18.0, TYPE="muon spoiler"
  D3a   : DRIF, L=5.7164

! energy spectrometer matching

  DSPECT : DRIF, L=0.5
  DECBPM : DRIF, L=1.4
  LX0h   : DRIF, L=LX0[L]/2

! ==============================================================================
! MARKers
! ------------------------------------------------------------------------------

  BEGEFF1 : MARK
  BCOL1   : MARK
  BCOL2   : MARK
  MDW     : MARK
  ESPECT  : MARK
  IP      : MARK
  ENDEFF1 : MARK

! for R-matrix fitting

  MR1i : MARK
  MR1f : MARK
  MR2i : MARK
  MR2f : MARK

! ==============================================================================
! diagnostic/correction devices, etc.
! ------------------------------------------------------------------------------

  PHASEMON : INST !beam arrival phase monitor
  WS_DIV   : WIRE !divergence wire scanner
  WS_ESP   : WIRE !energy spread wire scanner

! insertable beam stoppers

  ST1 : MARK !PPS
  ST2 : MARK !PPS
  ST3 : MARK !PPS
  ST4 : MARK !tune-up (1 bunch @ 750 GeV, 10 Hz = 20 kW)

! crab cavity (S-band, 2pi/3 device based on SLAC LOLA-III models)

  CRAB_CAV : DRIF, L=1.26

! steering magnets

  XCORFB : HKICK
  YCORFB : VKICK

! correctors for IP collision control

  XCORIP : HKICK
  YCORIP : VKICK

! virtual IP diagnostics (for simulations only)

  QDMY_IP : QUAD, L=1.E-06, K1=0, APER=1, TYPE="VIRTUAL"
  BPM_IP  : MONI, TYPE="DEFLECTION" !for deflection monitoring simulations
  WS_IP   : WIRE !IP beam size via lumi or deflection scans
  IP_PKG  : LINE=(QDMY_IP,BPM_IP,QDMY_IP,WS_IP)

! energy spectrometer BPMs

  ECBPM1 : MONI
  ECBPM2 : MONI
  ECBPM3 : MONI

! ==============================================================================
! BEAMLINEs
! ------------------------------------------------------------------------------

  QF1PKG : LINE=(QF1,BPMQ079,QF1)
  QD0PKG : LINE=(QD0,BPMQ079,QD0)

  EFFu : LINE=(BEGEFF1,&
               BPMMB079,XCORFB,YCORFB,MR1i,SP1,DBMa,&
               QB3,BPMQ079,QB3,DB0,&
               QB2,BPMQ079,QB2,DB1a,ST1,DB1b,&
               QB1,BPMQ079,QB1,DB2,&
               QB0,BPMQ079,QB0,DBM,BCOL1,BPMMB079,XCORFB,YCORFB,AB2,SP2,DBMb,&
               QB0,BPMQ079,QB0,DB2,&
               QB1,BPMQ079,QB1,DB1a,ST2,DB1c,PC1,DB1d,&
               QB2,BPMQ079,QB2,DB0,&
               QB3,BPMQ079,QB3,DBMM,MR1f,BPMMB079,XCORFB,YCORFB,AB3,SP3,DBMMa,&
               QB3,BPMQ079,QB3,DB0a,PC2,DB0b,&
               QB2,BPMQ079,QB2,DB1a,ST3,DB1c,PC3,DB1d,&
               QB1,BPMQ079,QB1,DB2,&
               QB0,BPMQ079,QB0,DBM,BCOL2,BPMMB079,XCORFB,YCORFB,AB4,SP4,DBMb,&
               QB0M,BPMQ079,QB0M,DB2a,PC4,DB2b,&
               QB1M,BPMQ079,QB1M,DB1Ma,PC5,DB1Mb,&
               QB2M,BPMQ079,QB2M,DB0Ma,BPMMB079,AB5,SP5,DB0Mb,&
               QB3M,BPMQ079,QB3M,LX0,&
               BS1,BS1,LX0,&
               BS1,BS1,LX0,&
               BS1,BS1,PC6,LX0a,&
               QS1,BPMQ079,QS1,LX0,&
               BS1,BS1,LX0,&
               BS1,BS1,LX0,&
               BS1,BS1,PDUMP,LX0a,BPME,&
               BS1,BS1,LX0,&
               BS1,BS1,LX0,BPME,&
               BS1,BS1,LX0,&
               BS1,BS1,LX0,BPME,&
               BS1,BS1,LX0,&
               BS1,BS1,LX0,BPME,WS_ESP,&
               BS1,BS1,LX0,&
               BS1,BS1,LX0,&
               BS1,BS1,PC7,LX0a,&
               QS2,BPMQ079,QS2,DS3,&
               QS3,BPMQ079,QS3,DSM,SPEX,DSMa,&
               QS3,BPMQ079,QS3,DS3a,PC8,DS3b,&
               QS2,BPMQ079,QS2,LX0,&
               BS1,BS1,LX0,&
               BS1,BS1,LX0,&
               BS1,BS1,LX0,&
               BS1,BS1,PC9,LX0a,&
               BS1,BS1,LX0,&
               BS1,BS1,LX0,&
               BS1,BS1,LX0,&
               BS1,BS1,PC10,LX0a,&
               BS1,BS1,LX0,&
               BS1,BS1,LX0,&
               BS1,BS1,LX0,&
               BS1,BS1,ABE,LX0b,&
               QS1,BPMQ079,QS1,LX0,&
               BS1,BS1,LX0,&
               BS1,BS1,LX0,&
               BS1,BS1,PC11,LX0a)

  EFFs : LINE=(MDW,DSPECT,&
               ECBPM1,DECBPM,BEC1a,BEC1b,DECBBo,BEC2a,BEC2b,DECBBi,ECBPM2,&
               ESPECT,DECBBi,BEC3a,BEC3b,DECBBo,BEC4a,BEC4b,DECBPM,ECBPM3,&
               DSPECT,MDW)

  EFFm : LINE=(QM16,BPMQ079,QM16,D12,&
               QM15,BPMQ079,QM15,D11,&
               QM14,BPMQ079,QM14,D10,&
               QM13a,BPMQ079,QM13a,LX0h,EFFs,LX0h,&
               QM13b,BPMQ079,QM13b,D9,&
               QM12,BPMQ079,QM12,D8,&
               QM11,BPMQ079,QM11)

  EFFd : LINE=(D7Ea,MUSP1,D7Ea,&
               OC7E,BPMVIRT,OC7E,D7D,&
               OC7D,BPMVIRT,OC7D,D7C,&
               OC7C,BPMVIRT,OC7C,D7B,&
               OC7B,BPMVIRT,OC7B,D7A,&
               OC7A,BPMVIRT,OC7A,D7,AB10,D710a,&
               QD10,BPMQ079,QD10,LX0,&
               OC10,BPMVIRT,OC10,LX0,WS_DIV,&
               QD10,BPMQ079,QD10,D6A,DEC6,BPMVIRT,DEC6,BPMMB079,D6,&
               QF9,BPMQ079,QF9,AB9,LX0b,&
               SF6,MR2i,BPMVIRT,SF6,BPMMB079,LX0,&
               QF9,BPMQ079,QF9,D5B,2*SBD_FF,LX0,2*SBD_FF,LX0,&
               B5,B5,LX0,&
               B5,B5,LX0,&
               B5,B5,LX0,&
               QD8,BPMQ079,QD8,LX0,&
               B5,B5,LX0,&
               B5,B5,AB7,LX0b,&
               QF7,BPMQ079,QF7,XCORFB,YCORFB,LX0,&
               B5,B5,LX0,&
               B5,B5,LX0,&
               QD6,BPMQ079,QD6,LX0,&
               B5,B5,LX0,&
               B5,B5,LX0,&
               B5,B5,D5A,&
               QF5,BPMQ079,QF5,LX0,&
               SF5,MR2f,BPMVIRT,SF5,BPMMB079,LX0,&
               QF5,BPMQ079,QF5,D4,DEC4,BPMVIRT,DEC4,D4A,&
               QD4,BPMQ079,QD4,LX0,&
               SD4,BPMVIRT,SD4,BPMMB079,LX0,&
               QD4,BPMQ079,QD4,OC4,D3a,MUSP2,D3c,LX0,&
               B2,B2,LX0,&
               B2,B2,LX0,&
               B2,B2,LX0,&
               B2,B2,LX0,&
               B2,B2,LX0,XCORFB,&
               B2,B2,LX0,&
               B2,B2,LX0,&
               QD2B,BPMQ079,QD2B,LX0,YCORFB,&
               B2,B2,LX0,&
               B2,B2,LX0,&
               QF3,BPMQ079,QF3,LX0,&
               B2,B2,LX0,&
               B2,B2,LX0,&
               QD2A,BPMQ079,QD2A,LX0,&
               B1,B1,LX0,&
               B1,B1,LX0,&
               B1,B1,LX0,&
               B1,B1,LX0,&
               B1,B1,LX0,&
               B1,B1,D2a,MSK1,D2b,CRAB_CAV,LX0,ST4,LX0,MSK2,DMSQ,&
               SQ3FF,SQ3FF,LX0,PHASEMON,&
               OC1,BPMVIRT,OC1,LX0,BPMMB079,&
               SF1,BPMVIRT,SF1,D1C,&
               QF1PKG,D1B,&
               SD0,BPMVIRT,SD0,BPMMB079,OC0,BPMVIRT,OC0,D1A,&
               QD0PKG,XCORIP,YCORIP,D0,IP,&
               ENDEFF1)

  EFF1  : LINE=(EFFu,EFFm,EFFd)

  EFF1S : LINE=(EFF1,IP_PKG)

! ==============================================================================

  RETURN
