  TITLE, "e- Collimation and Final Focus [20 mr] (ILCFF9)"

  OPTION, -INTER, -ECHO, VERIFY
  ASSIGN, PRINT="eff1.print"
  ASSIGN, ECHO="eff1.echo"

! ==============================================================================
! Common parameters and definitions
! ------------------------------------------------------------------------------

  Bsign := -1
  TFOC  :=  0

  BPMMB079 : MONI
  BPMQ079  : MONI
  BPME     : MONI
  BPMVIRT  : MONI

! ==============================================================================
! Load beamline definitions
! ------------------------------------------------------------------------------

  CALL, FILENAME="eff1.xsif"

! ==============================================================================
! Input beam definitions
! ------------------------------------------------------------------------------

! set up TWSS0

  Ef    := 250
  TBETX := 19.903055360355
  TALFX := 0
  TBETY := 23.515062700786
  TALFY := 0

  BXbcol := 6.130470999842E2 !600
  BYbcol := 9.787176221847E2 !1000

  BXip := 0.015
  BYip := 0.0004

  TWSS0 : BETA0, ENERGY=Ef, BETX=TBETX, ALFX=TALFX, BETY=TBETY, ALFY=TALFY
  TWSSM : BETA0, ENERGY=Ef, BETX=TBETX, ALFX=TALFX, BETY=TBETY, ALFY=TALFY

! set up BEAM

  EMITXn := 1.0e-5 
  EMITYn := 4.0e-8
  EMITX  := EMITXn/(Ef/EMASS) 
  EMITY  := EMITYn/(Ef/EMASS)
  BLENG  := 3.0e-4
  ESPRD  := 3.0e-3

  BEAM, PARTICLE=ELECTRON, ENERGY=Ef, EX=EMITX, EY=EMITY

! set up SIGMA0 for ENVELOPE plot (assumes DX=DPX=DY=DPY=0)

  EBETX := TBETX
  EALFX := TALFX
  EBETY := TBETY
  EALFY := TALFY
  EGAMX := (1+EALFX*EALFX)/EBETX
  EGAMY := (1+EALFY*EALFY)/EBETY
  SIG11 := EMITX*EBETX
  SIG21 := -EMITX*EALFX
  SIG22 := EMITX*EGAMX
  SIG33 := EMITY*EBETY
  SIG43 := -EMITY*EALFY
  SIG44 := EMITY*EGAMY
  C21   := SIG21/SQRT(SIG11*SIG22)
  C43   := SIG43/SQRT(SIG33*SIG44)
  SIG0  : SIGMA0, SIGX=SQRT(SIG11), SIGPX=SQRT(SIG22), R21=C21, &
                  SIGY=SQRT(SIG33), SIGPY=SQRT(SIG44), R43=C43, &
                  SIGT=BLENG, SIGPT=ESPRD

! ==============================================================================
! SUBROUTINEs
! ------------------------------------------------------------------------------

  M2BCOLL : SUBROUTINE

  ! match reversed FF to NLC beta collimation optics

    USE, (-EFF1)
    MATCH, BETX=BXip, BETY=BYip
      VARY, KQM16,  STEP=1.E-5
      VARY, KQM15,  STEP=1.E-5
      VARY, KQM14,  STEP=1.E-5
      VARY, KQM13a, STEP=1.E-5
      VARY, KQM13b, STEP=1.E-5
      VARY, KQM12,  STEP=1.E-5
      VARY, KQM11,  STEP=1.E-5
      VARY, dLmdw,  STEP=1.E-5, LOWER=-12.0
      CONSTR, BCOL2, MUX=2.760
      CONSTR, BCOL2, MUY=2.340
      CONSTR, BCOL2, BETX=BXbcol, ALFX=0, BETY=BYbcol, ALFY=0
      CONSTR, BCOL1, BETX=BXbcol, ALFX=0, BETY=BYbcol, ALFY=0
      LMDIF,  CALLS=20000, TOL=1.E-20
      MIGRAD, CALLS=20000, TOL=1.E-20
    ENDMATCH
    VALUE, KQM16,KQM15,KQM14,KQM13a,KQM13b,KQM12,KQM11
    VALUE, dLmdw,D12[L]
  ENDSUBROUTINE

! ------------------------------------------------------------------------------

  MTMAT : SUBROUTINE

  ! match 2nd order terms

    USE, EFF1
    MATCH, BETA0=TWSS0
      VARY, KSF6, STEP=1.E-6 !, LOWER=0
      VARY, KSF5, STEP=1.E-6 !, UPPER=0
      VARY, KSD4, STEP=1.E-6 !, LOWER=0
      VARY, KSF1, STEP=1.E-6 !, LOWER=0
      VARY, KSD0, STEP=1.E-6 !, UPPER=0
      TMATRIX, #S/#E, &
        TM(1,2,6)=0, TM(1,2,2)=0, TM(3,4,6)=0, TM(3,2,4)=0, TM(1,6,6)=0
      LMDIF, TOL=1.E-20
      MIGRAD, TOL=1.E-20
    ENDMATCH
    VALUE, KSF6/Bsign,KSF5/Bsign,KSD4/Bsign,KSF1/Bsign,KSD0/Bsign
  ENDSUBROUTINE

! ==============================================================================
! COMMANDs
! ------------------------------------------------------------------------------

  SETPLOT, XSIZE=25.4, YSIZE=20.32
 !SETPLOT, LWIDTH=5, LSCALE=1.5,SSCALE=1.5,RSCALE=1.5 
  OPTION, ECHO

! do matching

 !M2BCOLL
 !MTMAT

  COMMENT
    USE, EFF1
    SAVE, FILENAME="eff1_sv.mad"
  ENDCOMMENT

! generate output files and plots

  COMMENT
    USE, (-EFF1)
    PRINT, FULL
    TWISS, SAVE, BETX=BXip, BETY=BYip
    PLOT, TABLE=TWISS, HAXIS=S, VAXIS=RBETX,RBETY, &
      STYLE=100, SPLINE=.T., FILE="eff1"
  ENDCOMMENT

 !COMMENT
    USE, EFF1
    PRINT, FULL
    SAVEBETA, TWSSip, #E
    TWISS, BETA0=TWSS0, SAVE, TAPE="eff1_twiss.tape"
    VALUE, TWSSip[ENERGY]
    VALUE, TWSSip[BETX],TWSSip[ALFX],TWSSip[DX],TWSSip[DPX]
    VALUE, TWSSip[BETY],TWSSip[ALFY],TWSSip[DY],TWSSip[DPY]
    PLOT, TABLE=TWISS, HAXIS=S, VAXIS1=RBETX,RBETY, VAXIS2=DX, &
      STYLE=100, SPLINE=.T., FILE="eff1"
    PLOT, TABLE=TWISS, HAXIS=S, VAXIS1=RBETX,RBETY, VAXIS2=DX, &
      RANGE=EFFm, STYLE=100, SPLINE=.T., FILE="eff1"
    PLOT, TABLE=TWISS, HAXIS=S, VAXIS=MUX,MUY, &
      RANGE=EFFm, STYLE=100, SPLINE=.T., FILE="eff1"
 !ENDCOMMENT

  COMMENT
    USE, EFF1
    PRINT, FULL
    TWISS, CHROM, BETA0=TWSS0, SAVE
    PLOT, TABLE=TWISS, HAXIS=S, VAXIS=WX,WY, &
      STYLE=100, SPLINE=.T., FILE="eff1"
    PLOT, TABLE=TWISS, HAXIS=S, VAXIS=DDX,DDY, &
      STYLE=100, SPLINE=.T., FILE="eff1"
    PLOT, TABLE=TWISS, HAXIS=S, VAXIS=WX,WY, &
      RANGE=EFFm, STYLE=100, SPLINE=.T., FILE="eff1"
    PLOT, TABLE=TWISS, HAXIS=S, VAXIS=DDX,DDY, &
      RANGE=EFFm, STYLE=100, SPLINE=.T., FILE="eff1"
  ENDCOMMENT

  COMMENT
    USE, EFF1
    SELECT, OPTICS, FULL
    ENVELOPE, SIGMA0=SIG0, SAVE=ENVELOPE, TAPE="eff1_envelope.tape"
    PLOT, TABLE=ENVELOPE, HAXIS=S, VAXIS=SIGX,SIGY, &
      STYLE=100, SPLINE=.T., FILE="eff1"
  ENDCOMMENT

  COMMENT
    Xi     := 0
    Zi     := 0
    THETAi := 0
    VALUE, Xi,Zi,THETAi
    USE, EFF1
    PRINT, FULL
    SURVEY, X0=Xi, Z0=Zi, THETA0=THETAi, TAPE="eff1_survey.tape"
  ENDCOMMENT

  COMMENT

  ! plot the bandwidth (+-1%)

    BX        := BXip
    AX        := 0
    BY        := BYip
    AY        := 0
    L_OVER_L0 := 1
    DP        := -0.01

    DO, TIMES=17
      USE, EFF1
      SAVEBETA, TWSSip, IP
      PRINT, IP
      TWISS, BETA0=TWSS0, DELTAP=DP
      SET, BX, TWSSip[BETX]
      SET, AX, TWSSip[ALFX]
      SET, BY, TWSSip[BETY]
      SET, AY, TWSSip[ALFY]
      SET, L_OVER_L0, SQRT(BXip*BYip/(TWSSip[BETX]*TWSSip[BETY]))
      PUSH, DP, L_OVER_L0, BX, AX, BY, AY
      SET, DP, DP+0.00125
    ENDDO
    ENDPUSH, SAVE="BW.dat"

   !PLOT, TABLE=SPECIAL, HAXIS=DP, VAXIS=L_OVER_L0, FILE="eff1"
   !PLOT, TABLE=SPECIAL, HAXIS=DP, VAXIS1=BX, VAXIS2=BY, &
   !  STYLE=100, FILE="eff1"
   !PLOT, TABLE=SPECIAL, HAXIS=DP, VAXIS1=AX, VAXIS2=AY, &
   !  STYLE=100, FILE="eff1"

  ! plot the bandwidth (+-2%)

    USE, EFF1
    TWISS, SAVE, BETA0=TWSS0, DELTAP=-0.02:0.02:0.002
    PLOT, TABLE=TWISS, HAXIS=DELTAP, VAXIS1=BETX, VAXIS2=BETY, &
      RANGE=#E, STYLE=100, FILE="eff1"
    PLOT, TABLE=TWISS, HAXIS=DELTAP, VAXIS1=ALFX, VAXIS2=ALFY, &
      RANGE=#E, STYLE=100, FILE="eff1"
    PLOT, TABLE=TWISS, HAXIS=DELTAP, VAXIS1=GAMX, VAXIS2=GAMY, &
      RANGE=#E, STYLE=100, FILE="eff1"
  ENDCOMMENT

  STOP
