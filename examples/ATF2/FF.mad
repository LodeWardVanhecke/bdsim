  TITLE "ATF2: Final Focus (FF6)"

  ASSIGN, PRINT="FF.print"
  ASSIGN, ECHO="FF.echo"
  OPTION, DOUBLE, -ECHO, VERIFY

! ==============================================================================
! initial conditions (entrance to diagnostic chicane)
! ------------------------------------------------------------------------------

  E0d   := 1.542282        !design beam energy (GeV)
  E0    := 1.3             !nominal beam energy (GeV)
  EMITX := 2.0E-9          !horizontal emittance (m)
  EMITY := 2.0E-11         !vertical emittance (m)
  BLENG := 8.0E-3          !bunch length (m)
  ESPRD := 0.8E-3          !energy spread (1)
  TBETX := 2.117265291275  !twiss beta x (m)
  TALFX := -0.993178877762 !twiss alpha x
  TDX   := 0               !eta x (m)
  TDPX  := 0               !eta' x
  TBETY := 5.007839655133  !twiss beta y (m)
  TALFY := 1.923102274303  !twiss alpha y
  TDY   := 0               !eta y (m)
  TDPY  := 0               !eta' y

! ==============================================================================
! construct input beam matrix (assumes TDY=TDPY=0)
! ------------------------------------------------------------------------------

  EMITXn := EMITX*(E0/EMASS)
  EMITYn := EMITY*(E0/EMASS)

  BEAM, PARTICLE=ELECTRON, ENERGY=E0, EX=EMITX, EY=EMITY, &
        SIGT=BLENG, SIGE=ESPRD, NPART=2.0E+10

  TWSS0 : BETA0, BETX=TBETX, ALFX=TALFX, DX=TDX, DPX=TDPX, &
                 BETY=TBETY, ALFY=TALFY, DY=TDY, DPY=TDPY

  TGAMX := (1+TALFX*TALFX)/TBETX
  TGAMY := (1+TALFY*TALFY)/TBETY
  SIG11 := EMITX*TBETX+TDX*TDX*ESPRD*ESPRD
  SIG21 := -EMITX*TALFX+TDX*TDPX*ESPRD*ESPRD
  SIG22 := EMITX*TGAMX+TDPX*TDPX*ESPRD*ESPRD
  C21   := SIG21/SQRT(SIG11*SIG22)
  SIG33 := EMITY*TBETY
  SIG43 := -EMITY*TALFY
  SIG44 := EMITY*TGAMY
  C43   := SIG43/SQRT(SIG33*SIG44)
  SIG61 := TDX*ESPRD*ESPRD
  SIG62 := TDPX*ESPRD*ESPRD
  SIG66 := ESPRD*ESPRD
  C61   := SIG61/SQRT(SIG11*SIG66)
  C62   := SIG62/SQRT(SIG22*SIG66)
  SIG0  : SIGMA0, SIGX=SQRT(SIG11), SIGPX=SQRT(SIG22), R21=C21, &
                  SIGY=SQRT(SIG33), SIGPY=SQRT(SIG44), R43=C43, &
                  SIGT=BLENG      , SIGPT=ESPRD      , R61=C61, R62=C62

! ==============================================================================
! load XSIF file
! ------------------------------------------------------------------------------

  CALL, FILENAME="FF.xsif"

! ==============================================================================
! SUBROUTINEs
! ------------------------------------------------------------------------------

  MFF : SUBROUTINE

  ! match FF

    USE, FF
    MATCH, BETA0=TWSS0
      VARY, KLQM16, STEP=1.E-5
      VARY, KLQM15, STEP=1.E-5
      VARY, KLQM14, STEP=1.E-5
      VARY, KLQM13, STEP=1.E-5
      VARY, KLQM12, STEP=1.E-5
     !VARY, KLQM11, STEP=1.E-5
      WEIGHT, BETX=1, ALFX=1, BETY=10, ALFY=1
      CONSTR, IP, BETX=BXip, ALFX=0, BETY=BYip, ALFY=0, DY=0 !, DPY=0
     !WEIGHT, MUX=1.E-9, MUY=1.E-9
     !CONSTR, IP, MUX=2.2362, MUY=1.8485
      LMDIF,  CALLS=20000, TOL=1.E-20
      MIGRAD, CALLS=20000, TOL=1.E-20
    ENDMATCH
    VALUE, KLQM16,KLQM15,KLQM14,KLQM13,KLQM12,KLQM11
  ENDSUBROUTINE

! ------------------------------------------------------------------------------

  M2ND : SUBROUTINE

  ! match 2nd order terms

    USE, FF
    MATCH, BETA0=TWSS0
      VARY, KLSF6, STEP=1.E-6 !, LOWER=0
      VARY, KLSF5, STEP=1.E-6 !, UPPER=0
      VARY, KLSD4, STEP=1.E-6 !, LOWER=0
      VARY, KLSF1, STEP=1.E-6 !, LOWER=0
      VARY, KLSD0, STEP=1.E-6 !, UPPER=0
      TMATRIX, #S/#E, &
        TM(1,2,6)=0, TM(1,2,2)=0, TM(3,4,6)=0, TM(3,2,4)=0, TM(1,6,6)=0
      LMDIF, TOL=1.E-20
      MIGRAD, TOL=1.E-20
    ENDMATCH
    VALUE, KLSF6,KLSF5,KLSD4,KLSF1,KLSD0
  ENDSUBROUTINE

! ==============================================================================
! COMMANDs
! ------------------------------------------------------------------------------

  SETPLOT, XSIZE=25.4, YSIZE=20.32
  OPTION, ECHO

! do matching

 !MFF
 !M2ND

! generate twiss functions and plots

 !COMMENT
    USE, FF
    PRINT, FULL
    SAVEBETA, TWSSip, IP
    TWISS, COUPLE, BETA0=TWSS0, SAVE, TAPE="FF_twiss.tape"
    VALUE, TWSSip[ENERGY]
    VALUE, TWSSip[BETX],TWSSip[ALFX],TWSSip[DX],TWSSip[DPX]
    VALUE, TWSSip[BETY],TWSSip[ALFY],TWSSip[DY],TWSSip[DPY]
    PLOT, TABLE=TWISS, HAXIS=S, VAXIS1=RBETX,RBETY, VAXIS2=DX, &
      STYLE=100, SPLINE=.T., FILE="FF"
    PLOT, TABLE=TWISS, HAXIS=S, VAXIS=MUX,MUY, &
      STYLE=100, SPLINE=.T., FILE="FF"
 !ENDCOMMENT

! generate chromatic functions and plots

 !COMMENT
    USE, FF
    PRINT, FULL
    TWISS, CHROM, BETA0=TWSS0, SAVE, TAPE="FF_chrom.tape"
    PLOT, TABLE=TWISS, HAXIS=S, VAXIS=WX,WY, &
      STYLE=100, SPLINE=.T., FILE="FF"
    PLOT, TABLE=TWISS, HAXIS=S, VAXIS=DDX,DDY, &
      STYLE=100, SPLINE=.T., FILE="FF"
 !ENDCOMMENT

! generate envelope and plots

  COMMENT
    USE, FF
    PRINT, FULL
    SELECT, OPTICS, FULL
    ENVELOPE, SIGMA0=SIG0, SAVE, TAPE="FF_envelope.tape"
    XUM : STRING, "1.E6*SIGX"
    YUM : STRING, "1.E6*SIGY"
    PLOT, TABLE=ENVELOPE, HAXIS=S, VAXIS1=XUM, VAXIS2=YUM, &
      STYLE=100, SPLINE=.T., FILENAME="ATF2", &
      TITLE="<G>e<G><?>x<?>=2e-9 m, <G>e<G><?>y<?>=2e-11 m"
  ENDCOMMENT

! generate survey

  COMMENT
    USE, FF
    PRINT, FULL
    SURVEY, TAPE="FF_survey.tape"
  ENDCOMMENT

! generate OPTICS output

  COMMENT
    USE, FF
    SELECT, OPTICS, FULL
    OPTICS, BETA0=TWSS0, FILE="FF.optics", &
      COLUMN=NAME,KEYWORD,S,L,K0L,K1L,K2L, &
      ALFX,BETX,MUX,DX,DPX,ALFY,BETY,MUY,DY,DPY
  ENDCOMMENT

! plot the bandwidth (+-1%)

  COMMENT
    BX        := BXip
    AX        := 0
    BY        := BYip
    AY        := 0
    L_OVER_L0 := 1
    DP        := -0.01
    DO, TIMES=21
      USE, FF
      SAVEBETA, TWSSip, IP
      PRINT, IP
      TWISS, BETA0=TWSS0, DELTAP=DP
      SET, BX, TWSSip[BETX]
      SET, AX, TWSSip[ALFX]
      SET, BY, TWSSip[BETY]
      SET, AY, TWSSip[ALFY]
      SET, L_OVER_L0, SQRT(BXip*BYip/(TWSSip[BETX]*TWSSip[BETY]))
      PUSH, DP, L_OVER_L0, BX, AX, BY, AY
      SET, DP, DP+0.001
    ENDDO
    ENDPUSH, SAVE="BW.dat"
   !PLOT, TABLE=SPECIAL, HAXIS=DP, VAXIS=L_OVER_L0, FILE="FF"
   !PLOT, TABLE=SPECIAL, HAXIS=DP, VAXIS1=BX, VAXIS2=BY, &
   !  STYLE=100, FILE="FF"
   !PLOT, TABLE=SPECIAL, HAXIS=DP, VAXIS1=AX, VAXIS2=AY, &
   !  STYLE=100, FILE="FF"

    USE, FF
    TWISS, BETA0=TWSS0, SAVE, DELTAP=-0.01:0.01:0.001
    PLOT, TABLE=TWISS, HAXIS=DELTAP, VAXIS1=BETX, VAXIS2=BETY, &
      RANGE=#E, STYLE=100, FILE="FF", &
      VMIN=0,0, VMAX=0.02,0.0004
    PLOT, TABLE=TWISS, HAXIS=DELTAP, VAXIS1=ALFX, VAXIS2=ALFY, &
      RANGE=#E, STYLE=100, FILE="FF"
    PLOT, TABLE=TWISS, HAXIS=DELTAP, VAXIS1=GAMX, VAXIS2=GAMY, &
      RANGE=#E, STYLE=100, FILE="FF"
  ENDCOMMENT

  STOP
