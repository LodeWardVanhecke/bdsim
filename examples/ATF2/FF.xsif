! ==============================================================================
! 13-APR-2005, M. Woodley
!    From S. Kuroda's ff3.4 SAD deck and A. Seryi's ATFEXTRSHRTFF6 MAD deck
! ------------------------------------------------------------------------------

! global parameters

  BXip := 0.004
  BYip := 0.0001

! scaling parameters

  COBVAL := 1.0
  CSEX   := 1.0/COBVAL
  COCT   := CSEX/COBVAL
  CDEC   := COCT/COBVAL

! ==============================================================================
! dipoles
! ------------------------------------------------------------------------------

! transport to final focus

  LBHC := 1.0
  GBHC := 0.01
  ABHC := 0.1696278543301

  BHCa : SBEN, L=LBHC/2, HGAP=GBHC, E2=0, FINT=0.5, FINTX=0
  BHCb : SBEN, L=LBHC/2, HGAP=GBHC, E1=0, FINT=0, FINTX=0.5

  BH5Xa : BHCa, ANGLE=-ABHC/2, E1=-ABHC/2
  BH5Xb : BHCb, ANGLE=-ABHC/2, E2=-ABHC/2
  BH1Ca : BHCa, ANGLE=-ABHC/4, E1=-ABHC/4
  BH1Cb : BHCb, ANGLE=-ABHC/4, E2=-ABHC/4
  BH2Ca : BHCa, ANGLE=+ABHC/4, E1=+ABHC/4
  BH2Cb : BHCb, ANGLE=+ABHC/4, E2=+ABHC/4
  BH3Ca : BHCa, ANGLE=+ABHC/2, E1=+ABHC/2
  BH3Cb : BHCb, ANGLE=+ABHC/2, E2=+ABHC/2

! soft bend

  LBFF  := 1.0
  GBFF1 := 0.01
  GBFF3 := 0.03

  BFF1h : SBEN, L=LBFF/2, HGAP=GBFF1, FINT=0.0, FINTX=0.0
  BFF3h : SBEN, L=LBFF/2, HGAP=GBFF3, FINT=0.0, FINTX=0.0

  AB5 := 0.025540942116
  AB2 := 0.017195219832
  AB1 := 0.021690729134

  B5 : BFF1h, ANGLE=AB5
  B2 : BFF3h, ANGLE=AB2
  B1 : BFF3h, ANGLE=AB1

! ==============================================================================
! quadrupoles
! ------------------------------------------------------------------------------

  LQFF := 0.2
  RQFF := 0.015

  QFFh : QUAD, L=LQFF/2, APER=RQFF

! transport to final focus

  KLQ1C :=  1.5917339565869
  KLQ2C :=  0.8940381557938
  KLQ3C := -0.6400322575285
  KLQ4C :=  0.8940338734138
  KLQ5C :=  1.5917449012346

  Q1C : QFFh, K1=KLQ1C/LQFF
  Q2C : QFFh, K1=KLQ2C/LQFF
  Q3C : QFFh, K1=KLQ3C/LQFF
  Q4C : QFFh, K1=KLQ4C/LQFF
  Q5C : QFFh, K1=KLQ5C/LQFF

! final focus

  KLQM16 := -1.099943237907 !-0.275928153825
  KLQM15 :=  1.292218610426 ! 0.898218718591
  KLQM14 := -1.965388505214 !-1.696631103811
  KLQM13 :=  0.648166670389 ! 0.828833517102
  KLQM12 :=  0.854086280361 ! 0.84180880878
  KLQM11 :=  0
  KLQD10 := -0.313548300101
  KLQF9  :=  0.374074652373
  KLQD8  := -0.560633553863
  KLQF7  :=  0.5995239085
  KLQD6  := -0.560633553863
  KLQF5  :=  0.374074652373
  KLQD4  := -0.313548300101
  KLQD2B :=  0.588908646492
  KLQF3  := -0.2494251727
  KLQD2A := -0.274411188136

  QM16 : QFFh, K1=KLQM16/LQFF
  QM15 : QFFh, K1=KLQM15/LQFF
  QM14 : QFFh, K1=KLQM14/LQFF
  QM13 : QFFh, K1=KLQM13/LQFF
  QM12 : QFFh, K1=KLQM12/LQFF
  QM11 : QFFh, K1=KLQM11/LQFF
  QD10 : QFFh, K1=KLQD10/LQFF
  QF9  : QFFh, K1=KLQF9/LQFF
  QD8  : QFFh, K1=KLQD6/LQFF
  QF7  : QFFh, K1=KLQF7/LQFF
  QD6  : QFFh, K1=KLQD6/LQFF
  QF5  : QFFh, K1=KLQF5/LQFF
  QD4  : QFFh, K1=KLQD4/LQFF
  QD2B : QFFh, K1=KLQD2B/LQFF
  QF3  : QFFh, K1=KLQF3/LQFF
  QD2A : QFFh, K1=KLQD2A/LQFF

! final doublet

  LQF1 := 0.4
  LQD0 := 0.5

  KLQF1 :=  0.913516557464
  KLQD0 := -1.494423007975

  QF1 : QUAD, L=LQF1/2, K1=KLQF1/LQF1, APER=RQFF
  QD0 : QUAD, L=LQD0/2, K1=KLQD0/LQD0, APER=RQFF

! ==============================================================================
! sextupoles
! ------------------------------------------------------------------------------

  LSFF := 0.2
  RS15 := 0.015

  SFFh : SEXT, L=LSFF/2, APER=RS15

  KLSF6 := -11.880330162074 !-12.129243668335
  KLSF5 :=   0.329477211629 !  0.235864100914
  KLSD4 := -16.097076530693 !-16.044416419518

  SF6 : SFFh, K2=KLSF6/LSFF
  SF5 : SFFh, K2=KLSF5/LSFF
  SD4 : SFFh, K2=KLSD4/LSFF

! final sextupoles

  LSF1 := 0.2
  LSD0 := 0.2

  KLSF1 :=  3.382841921046 ! 3.400000367444
  KLSD0 := -5.023221141621 !-4.975257569454

  SF1 : SEXT, L=LSF1/2, K2=KLSF1/LSF1, APER=RS15
  SD0 : SEXT, L=LSD0/2, K2=KLSD0/LSD0, APER=RS15

! ==============================================================================
! octupoles
! ------------------------------------------------------------------------------

  LOC10 := 0.2
  LOC1  := 0.3
  LOC0  := 0.1
  RO15  := 0.015

  KLOC10 := 0*COCT
  KLOC1  := 0*COCT
  KLOC0  := 0*COCT

  OC10 : OCTU, L=LOC10/2, K3=KLOC10/LOC10, APER=RO15
  OC1  : OCTU, L=LOC1/2,  K3=KLOC1/LOC1,   APER=RO15
  OC0  : OCTU, L=LOC0/2,  K3=KLOC0/LOC0,   APER=RO15

! ==============================================================================
! decapoles
! ------------------------------------------------------------------------------

  KLDEC := 0*CDEC

  DEC6 : MULT, LRAD=0.1E-3, K4L=KLDEC
  DEC4 : MULT, LRAD=0.1E-3, K4L=KLDEC

! ==============================================================================
! drifts
! ------------------------------------------------------------------------------

! transport to final focus

  L1C  : DRIF, L=0.3004723718591
  L2C  : DRIF, L=0.5401026705969
  L3C  : DRIF, L=1.389604938477
  L4C  : DRIF, L=0.8090435591629
  L5C  : DRIF, L=0.3001536774791
  L6C  : DRIF, L=0.3001536613278
  L7C  : DRIF, L=0.8090373974711
  L8C  : DRIF, L=1.3896051902214
  L9C  : DRIF, L=0.5401044139346
  L10C : DRIF, L=0.3004716102222
  L11C : DRIF, L=0.3

! final focus

  LX0  : DRIF, L=0.1
  DDD  : DRIF, L=0.15
  D12  : DRIF, L=1.0
  D11  : DRIF, L=1.0
  D10  : DRIF, L=1.0
  D9   : DRIF, L=1.0
  D8   : DRIF, L=1.1
  D7   : DRIF, L=1.0
  D6A  : DRIF, L=0.4
  D6   : DRIF, L=0.4
  D5B  : DRIF, L=0.4
  DDB  : DRIF, L=0.0
  D05M : DRIF, L=0.5
  D5A  : DRIF, L=0.4
  D4   : DRIF, L=0.4
  D4A  : DRIF, L=0.4
  D3   : DRIF, L=1.1
  D2   : DRIF, L=3.0
  D1CM : DRIF, L=0.025
  D1B  : DRIF, L=0.25
  D1A  : DRIF, L=0.1
  D0   : DRIF, L=1.0

! ==============================================================================
! MARKers
! ------------------------------------------------------------------------------

  BEGFF : MARK
  ICHI  : MARK
  VWXY  : MARK
  ST4   : MARK
  IP    : MARK
  ENDFF : MARK

! ==============================================================================
! BEAMLINEs
! ------------------------------------------------------------------------------

  BH5X : LINE=(BH5Xa,BH5Xb)
  BH1C : LINE=(BH1Ca,BH1Cb)
  BH2C : LINE=(BH2Ca,BH2Cb)
  BH3C : LINE=(BH3Ca,BH3Cb)

  ETOF : LINE=(ICHI,&
    BH5X,L1C,Q1C,Q1C,L2C,BH1C,L3C,Q2C,Q2C,L4C,BH1C,L5C,Q3C,&
    Q3C,L6C,BH2C,L7C,Q4C,Q4C,L8C,BH2C,L9C,Q5C,Q5C,L10C,BH3C,&
    L11C)

  FF : LINE=(BEGFF,&
    ETOF,LX0,DDD,&
    QM16,QM16,DDD,D12,DDD,&
    QM15,QM15,DDD,D11,DDD,&
    QM14,QM14,DDD,D10,DDD,&
    QM13,QM13,DDD,D9,DDD,&
    QM12,QM12,DDD,D8,DDD,&
    QM11,QM11,DDD,VWXY,D7,DDD,&
    QD10,QD10,DDD,LX0,&
    OC10,OC10,LX0,DDD,&
    QD10,QD10,DDD,D6A,&
    DEC6,D6,DDD,&
    QF9,QF9,DDD,LX0,&
    SF6,SF6,LX0,DDD,&
    QF9,QF9,DDD,D5B,DDB,D05M,D05M,DDB,LX0,DDD,&
    QD8,QD8,DDD,LX0,DDB,D05M,D05M,DDB,LX0,DDD,&
    QF7,QF7,DDD,LX0,DDB,&
    B5,B5,DDB,LX0,DDD,&
    QD6,QD6,DDD,LX0,DDB,D05M,D05M,DDB,D5A,DDD,&
    QF5,QF5,DDD,LX0,&
    SF5,SF5,LX0,DDD,&
    QF5,QF5,DDD,D4,&
    DEC4,D4A,DDD,&
    QD4,QD4,DDD,LX0,&
    SD4,SD4,LX0,DDD,&
    QD4,QD4,DDD,D3,LX0,DDB,&
    B2,B2,DDB,LX0,DDD,&
    QD2B,QD2B,DDD,LX0,DDB,&
    B1,B1,DDB,LX0,DDD,&
    QF3,QF3,DDD,LX0,DDB,D05M,D05M,DDB,LX0,DDD,&
    QD2A,QD2A,DDD,LX0,DDB,D05M,D05M,DDB,D2,ST4,LX0,&
    OC1,OC1,D1CM,&
    SF1,SF1,D1CM,&
    OC0,OC0,D1CM,&
    QF1,QF1,D1B,&
    SD0,SD0,D1A,&
    QD0,QD0,D0,&
    IP,ENDFF)

! ==============================================================================

  RETURN
