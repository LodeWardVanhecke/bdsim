! ==============================================================================
! area=ebsyd
! ------------------------------------------------------------------------------
! 20-MAY-2003, M. Woodley
!    NLC2003 configuration (250 GeV beam)
! ------------------------------------------------------------------------------

! ==============================================================================
! SBENs
! ------------------------------------------------------------------------------

! single beam dumpers (there are 5 @ 1 mrad each)

  LSBD := 3.0
  ASBD := -Bsign*0.001 ; BSBD := Brho*ASBD/LSBD/Efact

  SBD  : SBEN, L=LSBD/2, ANGLE=ASBD/2

! horizontal dump bends

  LHBD  := 3.0
  AHBD0 := -Bsign*0.128647312448E-2 ; BHBD0 := Brho*AHBD0/LHBD/Efact
  AHBD1 := -Bsign*0.001             ; BHBD1 := Brho*AHBD1/LHBD/Efact

  HBD0 : SBEN, L=LHBD/2, ANGLE=AHBD0/2
  HBD1 : SBEN, L=LHBD/2, ANGLE=AHBD1/2

! vertical dump bends

  LVBD := 3.0
  AVBD := 0.00125 ; BVBD := Brho*AVBD/LVBD/Efact

  VBD : SBEN, L=LVBD/2, ANGLE=AVBD/2, TILT

! ==============================================================================
! QUADs
! ------------------------------------------------------------------------------

! humongous aperture dump quads (to accomodate lots of energy variation)

  LQBSYD := 3.0
  rQBSYD := 0.04

  QBSYD : QUAD, L=LQBSYD/2, APER=rQBSYD

  BQBSYD1 := 4.753152295731*Efact
  BQBSYD2 := 4.670430352758*Efact
  BQBSYD3 := 5.188224615103*Efact
  BQBSYD4 := 4.9*Efact
  BQBSYD5 := -7.349999675309*Efact

  QBSYD1  : QBSYD, K1=BQBSYD1/(rQBSYD*Brho)
  QBSYD2  : QBSYD, K1=BQBSYD2/(rQBSYD*Brho)
  QBSYD3  : QBSYD, K1=BQBSYD3/(rQBSYD*Brho)
  QBSYD4  : QBSYD, K1=BQBSYD4/(rQBSYD*Brho)
  QBSYD5  : QBSYD, K1=BQBSYD5/(rQBSYD*Brho)

! ==============================================================================
! SEXTs
! ------------------------------------------------------------------------------

! humongous aperture dump sextupoles (to correct DDX and DDPX)

  LSBSYD := 1.0
  rSBSYD := 0.04

  SBSYD : SEXT, L=LSBSYD/2, APER=rSBSYD

  BSBSYD1 := -Bsign*3.050282702899*Efact
  BSBSYD2 := -Bsign*4.62457173365*Efact
  BSBSYD3 := -7.8*Efact

  SBSYD1 : SBSYD, K2=2*BSBSYD1/(rSBSYD*rSBSYD*Brho)
  SBSYD2 : SBSYD, K2=2*BSBSYD2/(rSBSYD*rSBSYD*Brho)
  SBSYD3 : SBSYD, K2=2*BSBSYD3/(rSBSYD*rSBSYD*Brho), TILT

! ==============================================================================
! OCTUs
! ------------------------------------------------------------------------------

! humongous aperture dump octupoles (to correct high order dispersion)

  LOBSYD := 1.0
  rOBSYD := 0.04

  OBSYD : OCTU, L=LOBSYD/2, APER=rOBSYD

  BOBSYD1 := 0.52*Efact
  BOBSYD2 := 1.54*Efact
  BOBSYD3 := 8.0*Efact

  OBSYD1 : OBSYD, K3=6*BOBSYD1/(rOBSYD*rOBSYD*rOBSYD*Brho)
  OBSYD2 : OBSYD, K3=6*BOBSYD2/(rOBSYD*rOBSYD*rOBSYD*Brho)
  OBSYD3 : OBSYD, K3=6*BOBSYD3/(rOBSYD*rOBSYD*rOBSYD*Brho)

! ==============================================================================
! DRIFTs
! ------------------------------------------------------------------------------

  DBD0  : DRIF, L=0.5
  DBD1  : DRIF, L=23.5
  DHBD0 : DRIF, L=2.95
  DBD2  : DRIF, L=23.0
  DBD3  : DRIF, L=3.5
  DBD4  : DRIF, L=43.5
  DBD5  : DRIF, L=4.0
  DBD6  : DRIF, L=53.1

  DBD2o : DRIF, L=DBD2[L]-(LOBSYD+DBD0[L])
  DBD3s : DRIF, L=DBD3[L]-(LSBSYD+DBD0[L])
  DBD5s : DRIF, L=DBD5[L]-2*(LSBSYD+DBD0[L])

! ==============================================================================
! MARKERs
! ------------------------------------------------------------------------------

  BEGEBSYD : MARK
  FIT1     : MARK
  FIT2     : MARK
  FIT3     : MARK
  FIT4     : MARK
  DUMP     : MARK
  ENDEBSYD : MARK

! ==============================================================================
! BEAMLINEs
! ------------------------------------------------------------------------------

  EBSYD : LINE=(BEGEBSYD,&
                SBD,SBD,DBD0,&
                SBD,SBD,DBD0,&
                SBD,SBD,DBD0,&
                SBD,SBD,DBD0,&
                SBD,SBD,DBD1,&
                QBSYD1,QBSYD1,DBD0,&
                SBSYD1,FIT1,SBSYD1,DBD0,&
                QBSYD1,QBSYD1,DBD2o,&
                OBSYD1,OBSYD1,DBD0,&
                HBD0,HBD0,DBD0,&
                HBD0,HBD0,DHBD0,&
                HBD0,HBD0,DBD0,&
                HBD0,HBD0,DBD0,&
                OBSYD2,OBSYD2,DBD0,&
                SBSYD2,SBSYD2,DBD0,&
                QBSYD2,QBSYD2,DBD0,&
                QBSYD2,QBSYD2,DBD0,&
                SBSYD2,SBSYD2,DBD3s,&
                HBD0,HBD0,DBD0,&
                HBD0,HBD0,DHBD0,&
                HBD0,HBD0,DBD0,&
                HBD0,HBD0,DBD0,&
                OBSYD1,OBSYD1,DBD2o,&
                QBSYD1,QBSYD1,DBD0,&
                SBSYD1,FIT2,SBSYD1,DBD0,&
                QBSYD1,QBSYD1,DBD1,&
                HBD1,HBD1,DBD0,&
                HBD1,HBD1,DBD0,&
                HBD1,HBD1,DBD0,&
                HBD1,HBD1,DBD0,&
                HBD1,HBD1,FIT3,DBD0,&
                QBSYD4,QBSYD4,DBD4,&
                VBD,VBD,DBD0,&
                VBD,VBD,DBD0,&
                VBD,VBD,DBD0,&
                VBD,VBD,DBD0,&
                VBD,VBD,DBD5s,&
                OBSYD3,OBSYD3,DBD0,&
                OBSYD3,OBSYD3,DBD0,&
                QBSYD5,QBSYD5,DBD0,&
                SBSYD3,SBSYD3,DBD0,&
                QBSYD5,QBSYD5,DBD0,&
                SBSYD3,SBSYD3,DBD0,&
                SBSYD3,SBSYD3,DBD0,&
                QBSYD5,QBSYD5,DBD0,&
                SBSYD3,SBSYD3,DBD0,&
                QBSYD5,QBSYD5,DBD0,&
                OBSYD3,OBSYD3,DBD0,&
                OBSYD3,OBSYD3,DBD5s,&
                VBD,VBD,DBD0,&
                VBD,VBD,DBD0,&
                VBD,VBD,DBD0,&
                VBD,VBD,DBD0,&
                VBD,VBD,FIT4,DBD6,DUMP,&
                ENDEBSYD)

! ==============================================================================

  RETURN
