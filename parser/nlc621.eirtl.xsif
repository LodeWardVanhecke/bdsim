! ==============================================================================
! area=eirtl
! ------------------------------------------------------------------------------
! 21-JUL-2003, M. Woodley
!    NLC2003 interim configuration (250 GeV beam)
! ------------------------------------------------------------------------------

! matching conditions at entrance to final focus

  MBETX := 33.333/SQRT(2)
  MALFX := 0
  MBETY := 5.3333/SQRT(2)
  MALFY := 0

! ==============================================================================
! SBENs
! ------------------------------------------------------------------------------

! combined function bends

  DTHETA := 0.517031305019E-3

  THETA := (0.01392273916699+DTHETA)*Bsign
  Nc    := 10 !see definition of BIGBEND beamline, too ...
  ACFB  := THETA/(8*Nc+24)
  LCFB  := 2.0
  K0    := ACFB/LCFB
  BCFB  := Brho*K0
  GCFB  := 0.0127
  KCFBF := 0.02869716981
  KCFBD := -KCFBF

  CFBD1 : SBEN, L=LCFB/2, ANGLE=ACFB/2, HGAP=GCFB/2, K1=KCFBD, &
                E1=ACFB/2, FINT=0.5, FINTX=0
  CFBD2 : SBEN, L=LCFB/2, ANGLE=ACFB/2, HGAP=GCFB/2, K1=KCFBD, &
                E2=ACFB/2, FINT=0, FINTX=0.5
  CFBF1 : SBEN, L=LCFB/2, ANGLE=ACFB/2, HGAP=GCFB/2, K1=KCFBF, &
                E1=ACFB/2, FINT=0.5, FINTX=0
  CFBF2 : SBEN, L=LCFB/2, ANGLE=ACFB/2, HGAP=GCFB/2, K1=KCFBF, &
                E2=ACFB/2, FINT=0, FINTX=0.5

! ==============================================================================
! QUADs
! ------------------------------------------------------------------------------

! beta match (diagnostic section to big bend)

  BQMBB1 :=  3.879796147122*Efact
  BQMBB2 := -9.704848568147*Efact
  BQMBB3 :=  4.870723574596*Efact
  BQMBB4 :=  7.106397602575*Efact

  QMBB1 : QBDS3, K1=BQMBB1*QFACT3
  QMBB2 : QBDS3, K1=BQMBB2*QFACT3
  QMBB3 : QBDS3, K1=BQMBB3*QFACT3
  QMBB4 : QBDS3, K1=BQMBB4*QFACT3

! big bend

  LQC := 1.0
  LQ  := 2.0
  rQ  := 0.04/2

  BQFBC   := 8.25293772999*Efact
  BQFBDSi := 7.337336316044*Efact
  BQFBDSo := 7.430728917143*Efact

  KQFBC   := (BQFBC/rQ)/Brho
  KQFBDSi := (BQFBDSi/rQ)/Brho
  KQFBDSo := (BQFBDSo/rQ)/Brho

  QFBC   : QUAD, L=LQC/2, K1=KQFBC,   APER=rQ
  QFBDSi : QUAD, L=LQ/2,  K1=KQFBDSi, APER=rQ
  QFBDSo : QUAD, L=LQ/2,  K1=KQFBDSo, APER=rQ

! match/stretch/match

  LQFF2 := 2.0
  rQFF2 := 0.01

  BQX90  :=  5.283363695496*Efact
  BQMXC1 :=  8.588038033144*Efact
  BQMXC2 := -5.643557828455*Efact
  BQMXC3 :=  6.655735036449*Efact
  BQMFF1 :=  9.103929362806*Efact
  BQMFF2 := -7.154056434706*Efact
  BQMFF3 := -7.246981157232*Efact
  BQMFF4 :=  4.438601657006*Efact

  KQX90F := (BQX90/rQFF2)/Brho
  KQX90D := -KQX90F
  KQMXC1 := (BQMXC1/rQFF2)/Brho
  KQMXC2 := (BQMXC2/rQFF2)/Brho
  KQMXC3 := (BQMXC3/rQFF2)/Brho
  KQMFF1 := (BQMFF1/rQFF2)/Brho
  KQMFF2 := (BQMFF2/rQFF2)/Brho
  KQMFF3 := (BQMFF3/rQFF2)/Brho
  KQMFF4 := (BQMFF4/rQFF2)/Brho

  QX90F  : QUAD, L=LQFF2/2, K1=KQX90F, APER=rQFF2
  QX90D  : QUAD, L=LQFF2/2, K1=KQX90D, APER=rQFF2
  QMXC1a : QUAD, L=LQFF2/2, K1=KQMXC1, APER=rQFF2
  QMXC1b : QUAD, L=LQFF2/2, K1=KQMXC1, APER=rQFF2
  QMXC2a : QUAD, L=LQFF2/2, K1=KQMXC2, APER=rQFF2
  QMXC2b : QUAD, L=LQFF2/2, K1=KQMXC2, APER=rQFF2
  QMXC3  : QUAD, L=LQFF2/2, K1=KQMXC3, APER=rQFF2
  QMFF1  : QUAD, L=LQFF2/2, K1=KQMFF1, APER=rQFF2
  QMFF2  : QUAD, L=LQFF2/2, K1=KQMFF2, APER=rQFF2
  QMFF3  : QUAD, L=LQFF2/2, K1=KQMFF3, APER=rQFF2
  QMFF4  : QUAD, L=LQFF2/2, K1=KQMFF4, APER=rQFF2

! ==============================================================================
! DRIFTs
! ------------------------------------------------------------------------------

! beta match (diagnostic section to big bend)

  LMBB := 2.65

  DMBB0 : DRIF, L=LMBB-0.5
  DMBB1 : DRIF, L=LMBB
  DMBB2 : DRIF, L=0.5

! big bend

  LEB0  : DRIF, L=0.5
  LEB1i : DRIF, L=7.744902499528
  LEB1o : DRIF, L=7.457174332567
  LEB2  : DRIF, L=2.0
  LEB2h : DRIF, L=LEB2[L]/2
  LEB3  : DRIF, L=0.5

  LBB1  : DRIF, L=0.5
  LBB2  : DRIF, L=2.0
  LBB2h : DRIF, L=LBB2[L]/2

! match/stretch/match

  dLmdw1 := 1.282105385244

  LX90  := 21.0
  LMXC0 :=  0.5
  LMXC1 :=  1.0
  LMXC2 :=  5.65
  LMXC3 :=  1.0
  LMXC4 := 19.9662238
  LMFF1 :=  4.75
  LMFF2 :=  2.0
  LMFF3 :=  4.0
  LMFF4 :=  1.5

  DX90  : DRIF, L=LX90
  DMXC0 : DRIF, L=LMXC0
  DMXC1 : DRIF, L=LMXC1
  DMXC2 : DRIF, L=LMXC2+dLmdw1
  DMXC3 : DRIF, L=LMXC3
  DMXC4 : DRIF, L=LMXC4-dLmdw1
  DMFF1 : DRIF, L=LMFF1
  DMFF2 : DRIF, L=LMFF2
  DMFF3 : DRIF, L=LMFF3
  DMFF4 : DRIF, L=LMFF4

! stretch (geometry layout only)

  LEGEOL := 0 !ENDBB -> ENDEIRTL

  EGEOL : DRIF, L=LEGEOL

! ==============================================================================
! MARKs
! ------------------------------------------------------------------------------

  BEGEIRTL : MARK
  BEGBB    : MARK
  ENDBB    : MARK
  ENDEIRTL : MARK

! ==============================================================================
! BEAMLINEs
! ------------------------------------------------------------------------------

! beta match (diagnostic section to big bend)

  DGBB : LINE=(DMBB0,&
               QMBB1,QMBB1,DMBB1,&
               QMBB2,QMBB2,DMBB1,&
               QMBB3,QMBB3,DMBB1,&
               QMBB4,QMBB4,DMBB2)

! big bend

  CFBD    : LINE=(CFBD1,CFBD2)
  CFBF    : LINE=(CFBF1,CFBF2)
  BBCELL  : LINE=(QFBC,LBB1,2*(CFBF,LBB1),4*(CFBD,LBB1),&
                  CFBF,LBB1,CFBF,LBB2,QFBC)
  BBCELi  : LINE=(2*(CFBF,LBB1),4*(CFBD,LBB1),&
                  CFBF,LBB1,CFBF,LBB2,QFBC)
  BBCELo  : LINE=(QFBC,LBB1,2*(CFBF,LBB1),4*(CFBD,LBB1),&
                  CFBF,LBB1,CFBF)
  BBDSi   : LINE=(CFBD,3*(LEB0,CFBD),LEB1i,2*QFBDSi,LEB2,2*QFBDSi,LEB3)
  BBDSo   : LINE=(LEB3,2*QFBDSo,LEB2,2*QFBDSo,LEB1o,3*(CFBD,LEB0),CFBD)
  BIGBEND : LINE=(BEGBB,BBDSi,BBCELi,10*BBCELL,BBCELo,BBDSo,ENDBB)

! stretch FODO cell

  XC90 : LINE=(QX90D,DX90,QX90F,QX90F,DX90,QX90D)

! beta match (big bend to stretch)

  BBXC : LINE=(DMXC0,&
               QMXC1a,QMXC1a,DMXC1,&
               QMXC1b,QMXC1b,DMXC2,&
               QMXC2a,QMXC2a,DMXC3,&
               QMXC2b,QMXC2b,DMXC4,&
               QMXC3,QMXC3,DX90,&
               QX90D)

! beta match (stretch to final focus)

  XCFF : LINE=(QX90D,DX90,&
               QMFF1,QMFF1,DMFF1,&
               QMFF2,QMFF2,DMFF2,&
               QMFF3,QMFF3,DMFF3,&
               QMFF4,QMFF4,DMFF4)

! the whole thing

  EIRTL : LINE=(BEGEIRTL,&
                DMBB0,&
                QMBB1,BPMQ,MMOVER,QMBB1,DMBB1,&
                QMBB2,BPMQ,MMOVER,QMBB2,DMBB1,&
                QMBB3,BPMQ,MMOVER,QMBB3,DMBB1,&
                QMBB4,BPMQ,MMOVER,QMBB4,DMBB2,&
                BEGBB,&
                CFBD,3*(LEB0,CFBD),LEB1i,&
                QFBDSi,BPMQ157,MMOVER,QFBDSi,LEB2h,LEB2h,&
                QFBDSi,BPMQ157,MMOVER,QFBDSi,LEB3,&
                2*(CFBF,LBB1),4*(CFBD,LBB1),CFBF,LBB1,CFBF,LBB2h,LBB2h,&
                QFBC,BPMQ157,MMOVER,10*(QFBC,LBB1,&
                2*(CFBF,LBB1),4*(CFBD,LBB1),CFBF,LBB1,CFBF,LBB2h,LBB2h,&
                QFBC,BPMQ157,MMOVER),QFBC,LBB1,&
                2*(CFBF,LBB1),4*(CFBD,LBB1),CFBF,LBB1,CFBF,LEB3,&
                QFBDSo,BPMQ157,MMOVER,QFBDSo,LEB2h,LEB2h,&
                QFBDSo,BPMQ157,MMOVER,QFBDSo,LEB1o,&
                3*(CFBD,LEB0),CFBD,&
                ENDBB,&
                DMXC0,&
                QMXC1a,BPMQ079,MMOVER,QMXC1a,DMXC1,&
                QMXC1b,BPMQ079,MMOVER,QMXC1b,DMXC2,&
                QMXC2a,BPMQ079,MMOVER,QMXC2a,DMXC3,&
                QMXC2b,BPMQ079,MMOVER,QMXC2b,DMXC4,&
                QMXC3,BPMQ079,MMOVER,QMXC3,DX90,&
                QX90D,BPMQ079,MMOVER,QX90D,DX90,&
                QX90F,BPMQ079,MMOVER,QX90F,DX90,&
                QX90D,BPMQ079,MMOVER,QX90D,DX90,&
                QMFF1,BPMQ079,MMOVER,QMFF1,DMFF1,&
                QMFF2,BPMQ079,MMOVER,QMFF2,DMFF2,&
                QMFF3,BPMQ079,MMOVER,QMFF3,DMFF3,&
                QMFF4,BPMQ079,MMOVER,QMFF4,DMFF4,&
                ENDEIRTL)

! for geometry matching

  EIRTLG : LINE=(BEGEIRTL,DGBB,BIGBEND,EGEOL,ENDEIRTL)

! ==============================================================================

  RETURN
