! ==============================================================================
! area=effl
! ------------------------------------------------------------------------------
! 18-JUL-2003, M. Woodley
!    NLC2003 interim version (250 GeV beam); give spoilers and absorbers non-
!    zero physical lengths and set gaps as in EFFH; add PPS stoppers as in
!    EFFH; add 9 m long muon spoiler MUSP1 as in EFFH, but moved from the D7E
!    drift to the D7C drift ... there is simply no place to put the 18 m long
!    muon spoiler MUSP2; half-length SBDs to fit in half-length D5B drift ...
!    probably won't work; move crab cavity upstream of ST4
! 30-MAY-2003, M. Woodley
!    NLC2003 version (250 GeV beam); "ff2ir52903745pm.mad" from A. Seryi:
!
!    - short Raimondi/Seryi compact final focus system
!    - "short" superconducting final doublet optimized for 250 GeV beam
!    - bend strengths optimized for 250 GeV beam
!    - same-sense bending for energy collimation and Final Focus
!    - "large" detector with 5 T field;
!
!    flip signs of bends, sextupoles, and decapoles; split OC0 in half; change
!    "B2,B2V2" bend into two equal half-bends (same for "B2,B2V1")
! ------------------------------------------------------------------------------

! scaling parameters

  COBVAL := 1.0
  CFUDGE := 0.342
  COECOL := 1.0+CFUDGE*(1.0-COBVAL)
  CSEX   := 1.0/COBVAL
  COCT   := CSEX/COBVAL
  CDEC   := COCT/COBVAL

! ==============================================================================
! dipoles
! ------------------------------------------------------------------------------

  LBCO  := 8.57
  LB5   := 8.57
  LB2B  := 2.644512617272
  LCC   := 3.698850805634
  LB6   := 8.57
  LB6V2 := 9.041645938378
  LB6V1 := 8.226927206762

  GB1 := 0.01
  GB3 := 0.03

  BBS1 := +0.315564240306*Bsign*Efact*COECOL
  BB5  := +0.524054805069*Bsign*Efact*COBVAL
  BB2  := +0.468153104719*Bsign*Efact*COBVAL
  BB1  := +0.240195259406*Bsign*Efact*COBVAL

  CH1  := 1.284856936797
  CH2  := 0.679072950144
  CH5  := 0.849094416045
  MB2B := 1.097541593108

  TBS1  := BBS1*LBCO/Brho
  TB5   := CH5*BB5*LB5/Brho
  TB2B  := MB2B*CH2*BB2*(LB2B+2*LCC)/Brho
  TB2   := CH2*BB2*LB6/Brho
  TB2V2 := CH2*BB2*LB6V2/Brho
  TB2V1 := CH2*BB2*LB6V1/Brho
  TB1   := CH1*BB1*LB6/Brho

  BS1  : SBEN, L=LBCO/2,     ANGLE=TBS1/2,  HGAP=GB1, FINT=0, FINTX=0
  B5   : SBEN, L=LB5/2,      ANGLE=TB5/2,   HGAP=GB1, FINT=0, FINTX=0
  B2B  : SBEN, L=LB2B/2+LCC, ANGLE=TB2B/2,  HGAP=GB3, FINT=0, FINTX=0
  B2   : SBEN, L=LB6/2,      ANGLE=TB2/2,   HGAP=GB3, FINT=0, FINTX=0
  B2V2 : SBEN, L=LB6V2/2,    ANGLE=TB2V2/2, HGAP=GB3, FINT=0, FINTX=0
  B2V1 : SBEN, L=LB6V1/2,    ANGLE=TB2V1/2, HGAP=GB3, FINT=0, FINTX=0
  B1   : SBEN, L=LB6/2,      ANGLE=TB1/2,   HGAP=GB3, FINT=0, FINTX=0

! single beam dumper: sends the beam to a 120 Hz, 1 bunch/train dump (the SLC
! main dumps fit this bill); in the high-energy final focus a pair of PBH-type
! pulsed bends, 9 kG each, 3 m long each, is used to give a kick angle of
! 3.24 mrad ... since the drift space in which the SBDs live (D5B) has been
! shortened by a factor of 2 in this final focus, the SBDs have also been
! halved => *** THIS NEEDS TO BE LOOKED AT ***

  SBDFF : HKIC, L=1.5/2

! ==============================================================================
! quadrupoles
! ------------------------------------------------------------------------------

  LQ2  := 2.0
  LQD0 := 2.2
  LSQ3 := 0.3

  rQ1 := 0.01

  BQB0   :=   5.52967367306*Efact
  BQB1   :=  -6.344667660792*Efact
  BQB2   :=   5.840881249018*Efact
  BQB3   :=  -4.988824074902*Efact
  BQB0M  :=  -9.55567268772*Efact
  BQB1M  :=   7.39895048978*Efact
  BQB2M  :=  -0.310303905516*Efact
  BQB3M  :=  -3.510599096624*Efact
  BQS1   :=   2.579104988294*Efact
  BQS2   :=  -2.893761411878*Efact
  BQS3   :=   3.044204125368*Efact
  BQM16  :=  -1.708735543964*Efact
  BQM15  :=  -3.889245042244*Efact
  BQM14  :=  -0.357933792558*Efact
  BQM13  :=   6.3997967854*Efact
  BQM12  :=   8.019183155388*Efact
  BQM11  :=  -1.586785169222*Efact
  BQD10  :=  -2.268912506298*Efact
  BQF9   :=   2.802658802878*Efact
  BQD8   :=  -4.076032087338*Efact
  BQF7   :=   4.854988982411*Efact
  BQD6   :=  -4.076032087338*Efact
  BQF5   :=   2.802658802878*Efact
  BQD4   :=  -2.268912506298*Efact
  BQD2B  :=  -0.065308620232*Efact
  BQF3   :=   2.954839043516*Efact
  BQD2A  :=  -2.909571221435*Efact
  BSQ3FF :=   0*Efact
  BQF1   :=  16.47026675287*Efact
  BQD0   := -28.534438636006*Efact

  KQB0   := BQB0/rQ1/Brho
  KQB1   := BQB1/rQ1/Brho
  KQB2   := BQB2/rQ1/Brho
  KQB3   := BQB3/rQ1/Brho
  KQB0M  := BQB0M/rQ1/Brho
  KQB1M  := BQB1M/rQ1/Brho
  KQB2M  := BQB2M/rQ1/Brho
  KQB3M  := BQB3M/rQ1/Brho
  KQS1   := BQS1/rQ1/Brho
  KQS2   := BQS2/rQ1/Brho
  KQS3   := BQS3/rQ1/Brho
  KQM16  := BQM16/rQ1/Brho
  KQM15  := BQM15/rQ1/Brho
  KQM14  := BQM14/rQ1/Brho
  KQM13  := BQM13/rQ1/Brho
  KQM12  := BQM12/rQ1/Brho
  KQM11  := BQM11/rQ1/Brho
  KQD10  := BQD10/rQ1/Brho
  KQF9   := BQF9/rQ1/Brho
  KQD8   := BQD8/rQ1/Brho
  KQF7   := BQF7/rQ1/Brho
  KQD6   := BQD6/rQ1/Brho
  KQF5   := BQF5/rQ1/Brho
  KQD4   := BQD4/rQ1/Brho
  KQD2B  := BQD2B/rQ1/Brho
  KQF3   := BQF3/rQ1/Brho
  KQD2A  := BQD2A/rQ1/Brho
  KSQ3FF := BSQ3FF/rQ1/Brho
  KQF1   := BQF1/rQ1/Brho
  KQD0   := BQD0/rQ1/Brho

  QB0   : QUAD, L=LQ2/2,  K1=KQB0,   APER=rQ1
  QB1   : QUAD, L=LQ2/2,  K1=KQB1,   APER=rQ1
  QB2   : QUAD, L=LQ2/2,  K1=KQB2,   APER=rQ1
  QB3   : QUAD, L=LQ2/2,  K1=KQB3,   APER=rQ1
  QB0M  : QUAD, L=LQ2/2,  K1=KQB0M,  APER=rQ1
  QB1M  : QUAD, L=LQ2/2,  K1=KQB1M,  APER=rQ1
  QB2M  : QUAD, L=LQ2/2,  K1=KQB2M,  APER=rQ1
  QB3M  : QUAD, L=LQ2/2,  K1=KQB3M,  APER=rQ1
  QS1   : QUAD, L=LQ2/2,  K1=KQS1,   APER=rQ1
  QS2   : QUAD, L=LQ2/2,  K1=KQS2,   APER=rQ1
  QS3   : QUAD, L=LQ2/2,  K1=KQS3,   APER=rQ1
  QM16  : QUAD, L=LQ2/2,  K1=KQM16,  APER=rQ1
  QM15  : QUAD, L=LQ2/2,  K1=KQM15,  APER=rQ1
  QM14  : QUAD, L=LQ2/2,  K1=KQM14,  APER=rQ1
  QM13  : QUAD, L=LQ2/2,  K1=KQM13,  APER=rQ1
  QM12  : QUAD, L=LQ2/2,  K1=KQM12,  APER=rQ1
  QM11  : QUAD, L=LQ2/2,  K1=KQM11,  APER=rQ1
  QD10  : QUAD, L=LQ2/2,  K1=KQD10,  APER=rQ1
  QF9   : QUAD, L=LQ2/2,  K1=KQF9,   APER=rQ1
  QD8   : QUAD, L=LQ2/2,  K1=KQD6,   APER=rQ1
  QF7   : QUAD, L=LQ2/2,  K1=KQF7,   APER=rQ1
  QD6   : QUAD, L=LQ2/2,  K1=KQD6,   APER=rQ1
  QF5   : QUAD, L=LQ2/2,  K1=KQF5,   APER=rQ1
  QD4   : QUAD, L=LQ2/2,  K1=KQD4,   APER=rQ1
  QD2B  : QUAD, L=LQ2/2,  K1=KQD2B,  APER=rQ1
  QF3   : QUAD, L=LQ2/2,  K1=KQF3,   APER=rQ1
  QD2A  : QUAD, L=LQ2/2,  K1=KQD2A,  APER=rQ1
  SQ3FF : QUAD, L=LSQ3/2, K1=KSQ3FF, APER=rQ1, TILT
  QF1   : QUAD, L=LQ2/2,  K1=KQF1,   APER=rQ1
  QD0   : QUAD, L=LQD0/2, K1=KQD0,   APER=rQ1

! ==============================================================================
! sextupoles
! ------------------------------------------------------------------------------

  LS1  := 1.0
  LSF1 := 0.3
  LSD0 := 0.6

  rS1 := 0.01

  BSF6 := -1.97397233104*Bsign*Efact*CSEX
  BSF5 := +0.539923716866*Bsign*Efact*CSEX
  BSD4 := -2.98648283549*Bsign*Efact*CSEX
  BSF1 := +6.018361557878*Bsign*Efact*CSEX
  BSD0 := -5.77596406593*Bsign*Efact*CSEX

  KSF6 := 2*BSF6/(rS1*rS1)/Brho
  KSF5 := 2*BSF5/(rS1*rS1)/Brho
  KSD4 := 2*BSD4/(rS1*rS1)/Brho
  KSF1 := 2*BSF1/(rS1*rS1)/Brho
  KSD0 := 2*BSD0/(rS1*rS1)/Brho

  SF6 : SEXT, L=LS1/2,  K2=KSF6, APER=rS1
  SF5 : SEXT, L=LS1/2,  K2=KSF5, APER=rS1
  SD4 : SEXT, L=LS1/2,  K2=KSD4, APER=rS1
  SF1 : SEXT, L=LSF1/2, K2=KSF1, APER=rS1
  SD0 : SEXT, L=LSD0/2, K2=KSD0, APER=rS1

! ==============================================================================
! octupoles
! ------------------------------------------------------------------------------

  LO3  := 3.0
  LO2  := 2.0
  LO1  := 1.0
  LOp3 := 0.3
  LOp1 := 0.1

  rO1  := 0.01
  rOp7 := 0.007

 !VBOC10 := 0.107343710721    !TURTLE
 !VBOC1  := 0.068959932986    !TURTLE
 !VBOC0  := 0.317234789399E-4 !TURTLE

  VBOC10 := 0.107343710721+0.011 !DIMAD
  VBOC1  := 0.068959932986+0.050 !DIMAD
  VBOC0  := 0.317234789399E-4    !DIMAD

  BOC7E := 0*Efact
  BOC7D := 0*Efact
  BOC7C := 0*Efact
  BOC7B := 0*Efact
  BOC7A := 0*Efact
  BOC10 := VBOC10*Efact*COCT
  BOC1  := VBOC1*Efact*COCT
  BOC0  := VBOC0*Efact*COCT

  KOC7E := 6*BOC7E/(rOp7*rOp7*rOp7)/Brho
  KOC7D := 6*BOC7D/(rOp7*rOp7*rOp7)/Brho
  KOC7C := 6*BOC7C/(rOp7*rOp7*rOp7)/Brho
  KOC7B := 6*BOC7B/(rOp7*rOp7*rOp7)/Brho
  KOC7A := 6*BOC7A/(rOp7*rOp7*rOp7)/Brho
  KOC10 := 6*BOC10/(rOp7*rOp7*rOp7)/Brho
  KOC1  := 6*BOC1/(rO1*rO1*rO1)/Brho
  KOC0  := 6*BOC0/(rO1*rO1*rO1)/Brho

  OC7E : OCTU, L=LO3/2,  K3=KOC7E, APER=rOp7
  OC7D : OCTU, L=LO3/2,  K3=KOC7D, APER=rOp7
  OC7C : OCTU, L=LO2/2,  K3=KOC7C, APER=rOp7
  OC7B : OCTU, L=LO2/2,  K3=KOC7B, APER=rOp7
  OC7A : OCTU, L=LOp3/2, K3=KOC7A, APER=rOp7
  OC10 : OCTU, L=LO1/2,  K3=KOC10, APER=rOp7
  OC1  : OCTU, L=LOp3/2, K3=KOC1,  APER=rO1
  OC0  : OCTU, L=LOp1/2, K3=KOC0,  APER=rO1

! ==============================================================================
! decapoles
! ------------------------------------------------------------------------------

  KLDEC := -80000*Bsign*CDEC

  DEC4 : MULT, LRAD=1.0E-4, K4L=KLDEC
  DEC6 : MULT, LRAD=1.0E-4, K4L=KLDEC

! ==============================================================================
! spoilers and absorbers
! (NOTE: copied from EFFH ... not optimized for this system!)
! ------------------------------------------------------------------------------
! NOTEs: spoilers (SP*) and absorbers (AB*) are movable 4-jaw devices;
!        protection collimators (PC*) are fixed circular apertures;
!        photon masks (MSK*) are fixed vertical slits;
!        SP1-5 are 0.6 radiation lengths copper;
!        SPEX is 1.0 radiation length titanium;
!        AB2-5 are 30.0 radiation lengths copper;
!        ABE, AB7-10, and MSK1-2 are 30.0 radiation lengths tungsten;
!        PC1-11 are 15.0 radiation lengths copper;
!        special element TYPE attributes for GEANT translation via MAD OPTICS
!        output per G. Blair
! ------------------------------------------------------------------------------

! per L. Keller for tail-folding octupole doublets OFF

  XGAPSP1  :=  0.300e-3 ; YGAPSP1  :=  0.250e-3
  XGAPAB2  :=  4.000e-3 ; YGAPAB2  :=  4.000e-3
  XGAPSP2  :=  0.300e-3 ; YGAPSP2  :=  0.200e-3
  RPC1     :=  5.000e-3
  XGAPAB3  :=  4.000e-3 ; YGAPAB3  :=  4.000e-3
  XGAPSP3  :=  0.300e-3 ; YGAPSP3  :=  0.250e-3
  RPC2     :=  5.000e-3
  RPC3     :=  5.000e-3
  XGAPAB4  :=  4.000e-3 ; YGAPAB4  :=  4.000e-3
  XGAPSP4  :=  0.300e-3 ; YGAPSP4  :=  0.200e-3
  RPC4     :=  5.000e-3
  RPC5     :=  5.000e-3
  XGAPAB5  :=  4.000e-3 ; YGAPAB5  :=  4.000e-3
  XGAPSP5  :=  0.420e-3 ; YGAPSP5  :=  0.250e-3
  RPC6     :=  5.000e-3
  RPDUMP   := 10.000e-3
  RPC7     :=  5.000e-3
  XGAPSPEX :=  3.200e-3
  RPC8     :=  5.000e-3
  RPC9     :=  5.000e-3
  RPC10    :=  5.000e-3
  XGAPABE  :=  1.000e-3 ; YGAPABE  :=  1.000e-3
  RPC11    :=  5.000e-3
  XGAPAB10 :=  4.400e-3 ; YGAPAB10 :=  4.400e-3
  XGAPAB9  :=  6.600e-3 ; YGAPAB9  :=  3.000e-3
  XGAPAB7  :=  4.500e-3 ; YGAPAB7  :=  3.000e-3
  XGAPMSK1 :=  8.300e-3
  XGAPMSK2 :=  8.500e-3

! per L. Keller for tail-folding octupole doublets ON

 !SET, XGAPSP1, 0.750e-3 ; SET, YGAPSP1, 0.750e-3
 !SET, XGAPSP2, 0.750e-3 ; SET, YGAPSP2, 0.750e-3
 !SET, XGAPSP3, 0.750e-3 ; SET, YGAPSP3, 0.750e-3
 !SET, XGAPSP4, 0.750e-3 ; SET, YGAPSP4, 0.750e-3
 !SET, XGAPSP5, 0.750e-3 ; SET, YGAPSP5, 0.750e-3

  LSPB  := 0.0086 ! 0.6 r.l. Cu
  LSPE  := 0.0356 ! 1.0 r.l. Ti
  LAB   := 0.4290 !30.0 r.l. Cu
  LABFF := 0.1050 !30.0 r.l. W
  LPC   := 0.2145 !15.0 r.l. Cu
  LMSK  := 0.1050 !30.0 r.l. W

  SP1   : RCOL, L=LSPB,  TYPE="X0.6Y0.5",  XSIZE=XGAPSP1,  YSIZE=YGAPSP1
  AB2   : RCOL, L=LAB,   TYPE="X8Y8",      XSIZE=XGAPAB2,  YSIZE=YGAPAB2
  SP2   : RCOL, L=LSPB,  TYPE="X0.6Y0.4",  XSIZE=XGAPSP2,  YSIZE=YGAPSP2
  PC1   : ECOL, L=LPC,   TYPE="X10Y10",    XSIZE=RPC1,     YSIZE=RPC1
  AB3   : RCOL, L=LAB,   TYPE="X8Y8",      XSIZE=XGAPAB3,  YSIZE=YGAPAB3
  SP3   : RCOL, L=LSPB,  TYPE="X0.6Y0.5",  XSIZE=XGAPSP3,  YSIZE=YGAPSP3
  PC2   : ECOL, L=LPC,   TYPE="X10Y10",    XSIZE=RPC2,     YSIZE=RPC2
  PC3   : ECOL, L=LPC,   TYPE="X10Y10",    XSIZE=RPC3,     YSIZE=RPC3
  AB4   : RCOL, L=LAB,   TYPE="X8Y8",      XSIZE=XGAPAB4,  YSIZE=YGAPAB4
  SP4   : RCOL, L=LSPB,  TYPE="X0.6Y0.4",  XSIZE=XGAPSP4,  YSIZE=YGAPSP4
  PC4   : ECOL, L=LPC,   TYPE="X10Y10",    XSIZE=RPC4,     YSIZE=RPC4
  PC5   : ECOL, L=LPC,   TYPE="X10Y10",    XSIZE=RPC5,     YSIZE=RPC5
  AB5   : RCOL, L=LAB,   TYPE="X8Y8",      XSIZE=XGAPAB5,  YSIZE=YGAPAB5
  SP5   : RCOL, L=LSPB,  TYPE="X0.84Y0.5", XSIZE=XGAPSP5,  YSIZE=YGAPSP5
  PC6   : ECOL, L=LPC,   TYPE="X10Y10",    XSIZE=RPC6,     YSIZE=RPC6
  PDUMP : ECOL, L=LPC,   TYPE="X20Y20",    XSIZE=RPDUMP,   YSIZE=RPDUMP
  PC7   : ECOL, L=LPC,   TYPE="X10Y10",    XSIZE=RPC7,     YSIZE=RPC7
  SPEX  : RCOL, L=LSPE,  TYPE="X6.4Y0",    XSIZE=XGAPSPEX
  PC8   : ECOL, L=LPC,   TYPE="X10Y10",    XSIZE=RPC8,     YSIZE=RPC8
  PC9   : ECOL, L=LPC,   TYPE="X10Y10",    XSIZE=RPC9,     YSIZE=RPC9
  PC10  : ECOL, L=LPC,   TYPE="X10Y10",    XSIZE=RPC10,    YSIZE=RPC10
  ABE   : RCOL, L=LABFF, TYPE="X2Y2",      XSIZE=XGAPABE,  YSIZE=YGAPABE
  PC11  : ECOL, L=LPC,   TYPE="X10Y10",    XSIZE=RPC11,    YSIZE=RPC11
  AB10  : RCOL, L=LABFF, TYPE="X8.8Y8.8",  XSIZE=XGAPAB10, YSIZE=YGAPAB10
  AB9   : RCOL, L=LABFF, TYPE="X13.2Y6",   XSIZE=XGAPAB9,  YSIZE=YGAPAB9
  AB7   : RCOL, L=LABFF, TYPE="X9Y6",      XSIZE=XGAPAB7,  YSIZE=YGAPAB7
  MSK1  : RCOL, L=LMSK,  TYPE="X16.6Y0",   XSIZE=XGAPMSK1
  MSK2  : RCOL, L=LMSK,  TYPE="X17Y0",     XSIZE=XGAPMSK2

! ==============================================================================
! drifts
! ------------------------------------------------------------------------------

  LVW  := 0
  LMD2 := 35.965235321049

  DBM  : DRIF, L=1.5
  DB0  : DRIF, L=3.0
  DB1  : DRIF, L=35.0
  DB2  : DRIF, L=4.05
  DB1M : DRIF, L=13.6542105
  DB2M : DRIF, L=17.052903
  LX0  : DRIF, L=0.3
  DS3  : DRIF, L=6.0
  DSM  : DRIF, L=0.5
  D12  : DRIF, L=6.0
  D11  : DRIF, L=6.0
  D10  : DRIF, L=6.0
  DVWM : DRIF, L=LVW
  DVWP : DRIF, L=-LVW
  D9   : DRIF, L=11.0
  D8   : DRIF, L=10.91
  D7E  : DRIF, L=5.35
  D7D  : DRIF, L=2.0
  D7C  : DRIF, L=18.5
  D7B  : DRIF, L=1.5
  D7A  : DRIF, L=18.5
  D7   : DRIF, L=10.0
  D710 : DRIF, L=1.5
  D6A  : DRIF, L=0.433885
  D6   : DRIF, L=4.619995
  D5B  : DRIF, L=3.44767
  D5A  : DRIF, L=3.44767
  D4   : DRIF, L=4.619995
  D4A  : DRIF, L=0.433885
  D3   : DRIF, L=2.35006922311
  D3B  : DRIF, L=29.7-8*LCC
  CRAB : DRIF, L=1.26 !crab cavity (S-band, 2pi/3 based on SLAC LOLA-III)
  D2   : DRIF, L=LMD2
  D1C  : DRIF, L=0.15
  D1CM : DRIF, L=0.025
  D1B  : DRIF, L=1.35
  D1A  : DRIF, L=0.15
  D0   : DRIF, L=3.51

  DBMa  : DRIF, L=DBM[L]-LSPB
  DBMb  : DRIF, L=DBM[L]-(LAB+LSPB)
  DB1a  : DRIF, L=DB1[L]/2
  DB1b  : DRIF, L=DB1[L]-DB1a[L]
  DB1c  : DRIF, L=DB1[L]/2-0.5
  DB1d  : DRIF, L=DB1[L]-(DB1a[L]+DB1c[L]+LPC)
  DB0a  : DRIF, L=DB0[L]-0.5
  DB0b  : DRIF, L=0.5-LPC
  DB2a  : DRIF, L=DB2[L]-0.5
  DB2b  : DRIF, L=0.5-LPC
  DB1Ma : DRIF, L=DB1M[L]-0.5
  DB1Mb : DRIF, L=0.5-LPC
  LX0a  : DRIF, L=LX0[L]-LPC
  DSMa  : DRIF, L=DSM[L]-LSPE
  DS3a  : DRIF, L=DS3[L]-0.5
  DS3b  : DRIF, L=0.5-LPC
  LX0b  : DRIF, L=LX0[L]-LABFF
  MUSP1 : DRIF, L=9.0, TYPE="muon spoiler"
  D7Ca  : DRIF, L=(D7C[L]-MUSP1[L])/2
  D710a : DRIF, L=D710[L]-LABFF
  D5Ba  : DRIF, L=(D5B[L]-4*SBDFF[L])/3
  D2a   : DRIF, L=18.795
  D2b   : DRIF, L=16.245
  DMSQ  : DRIF, L=D2[L]-(D2a[L]+D2b[L]+CRAB[L]+2*SQ3FF[L]+2*LMSK+3*LX0[L])

! ==============================================================================
! MARKers
! ------------------------------------------------------------------------------

  BEGEFFL : MARK
  VWXY    : MARK !virtual waist location
  IP      : MARK
  ENDEFFL : MARK

! for fitting R-matrix elements

  MR1i : MARK
  MR1f : MARK
  MDXX : MARK

! ==============================================================================
! diagnostic/correction devices, etc.
! ------------------------------------------------------------------------------

  PHASEMON : INST !beam arrival phase monitor
  WS_DIV   : WIRE !divergence wire scanner
  WS_ESP   : WIRE !energy spread wire scanner

! insertable beam stoppers

  ST1 : MARK !PPS
  ST2 : MARK !PPS
  ST3 : MARK !PPS
  ST4 : MARK !tune-up (1 bunch @ 750 GeV, 10 Hz = 20 kW)

! steering magnets

  XCORFB : HKIC
  YCORFB : VKIC

! correctors for IP collision control

  XCORIP : HKIC
  YCORIP : VKIC

! virtual IP diagnostics (for simulations only)

  QDMY_IP : QUAD, L=1.E-06, K1=0, APER=1, TYPE="VIRTUAL"
  BPM_IP  : MONI, TYPE="DEFLECTION" !for deflection monitoring simulations
  WS_IP   : WIRE !IP beam size via lumi or deflection scans
  IP_PKG  : LINE=(QDMY_IP,BPM_IP,QDMY_IP,WS_IP)

! ==============================================================================
! BEAMLINEs
! ------------------------------------------------------------------------------

  EFFL  : LINE=(BEGEFFL,&
                BPMMB079,XCORFB,YCORFB,MR1i,SP1,DBMa,&
                QB0,BPMQ079,QB0,DB0,&
                QB1,BPMQ079,QB1,DB1a,ST1,DB1b,&
                QB2,BPMQ079,QB2,DB2,&
                QB3,BPMQ079,QB3,DBM,BPMMB079,XCORFB,YCORFB,AB2,SP2,DBMb,&
                QB3,BPMQ079,QB3,DB2,&
                QB2,BPMQ079,QB2,DB1a,ST2,DB1c,PC1,DB1d,&
                QB1,BPMQ079,QB1,DB0,&
                QB0,BPMQ079,QB0,DBM,MR1f,BPMMB079,XCORFB,YCORFB,AB3,SP3,DBMb,&
                QB0,BPMQ079,QB0,DB0a,PC2,DB0b,&
                QB1,BPMQ079,QB1,DB1a,ST3,DB1c,PC3,DB1d,&
                QB2,BPMQ079,QB2,DB2,&
                QB3,BPMQ079,QB3,DBM,BPMMB079,XCORFB,YCORFB,AB4,SP4,DBMb,&
                QB0M,BPMQ079,QB0M,DB2a,PC4,DB2b,&
                QB1M,BPMQ079,QB1M,DB1Ma,PC5,DB1Mb,&
                QB2M,BPMQ079,QB2M,DB2M,BPMMB079,AB5,SP5,DBMb,&
                QB3M,BPMQ079,QB3M,LX0,&
                BS1,BS1,LX0,&
                BS1,BS1,LX0,&
                BS1,BS1,PC6,LX0a,&
                QS1,BPMQ079,QS1,LX0,&
                BS1,BS1,LX0,&
                BS1,BS1,LX0,&
                BS1,BS1,PDUMP,LX0a,BPME,&
                BS1,BS1,LX0,&
                BS1,BS1,LX0,BPME,&
                BS1,BS1,LX0,&
                BS1,BS1,LX0,BPME,&
                BS1,BS1,LX0,&
                BS1,BS1,LX0,BPME,WS_ESP,&
                BS1,BS1,LX0,&
                BS1,BS1,LX0,&
                BS1,BS1,PC7,LX0a,&
                QS2,BPMQ079,QS2,DS3,&
                QS3,BPMQ079,QS3,DSM,SPEX,DSMa,&
                QS3,BPMQ079,QS3,DS3a,PC8,DS3b,&
                QS2,BPMQ079,QS2,LX0,&
                BS1,BS1,LX0,&
                BS1,BS1,LX0,&
                BS1,BS1,LX0,&
                BS1,BS1,PC9,LX0a,&
                BS1,BS1,LX0,&
                BS1,BS1,LX0,&
                BS1,BS1,LX0,&
                BS1,BS1,PC10,LX0a,&
                BS1,BS1,LX0,&
                BS1,BS1,LX0,&
                BS1,BS1,LX0,&
                BS1,BS1,ABE,LX0b,&
                QS1,BPMQ079,QS1,LX0,&
                BS1,BS1,LX0,&
                BS1,BS1,LX0,&
                BS1,BS1,MDXX,PC11,LX0a,&
                QM16,BPMQ079,QM16,D12,&
                QM15,BPMQ079,QM15,D11,&
                QM14,BPMQ079,QM14,D10,&
                QM13,BPMQ079,QM13,LX0,&
                QM13,BPMQ079,QM13,D9,&
                QM12,BPMQ079,QM12,D8,&
                QM11,BPMQ079,QM11,DVWM,VWXY,DVWP,D7E,&
                OC7E,OC7E,D7D,&
                OC7D,OC7D,D7Ca,MUSP1,D7Ca,&
                OC7C,OC7C,D7B,&
                OC7B,OC7B,D7A,&
                OC7A,OC7A,D7,AB10,D710a,&
                QD10,BPMQ079,QD10,LX0,&
                OC10,OC10,LX0,WS_DIV,&
                QD10,BPMQ079,QD10,D6A,&
                DEC6,BPMMB079,D6,&
                QF9,BPMQ079,QF9,AB9,LX0b,&
                SF6,SF6,BPMMB079,LX0,&
                QF9,BPMQ079,QF9,D5Ba,SBDFF,SBDFF,D5Ba,SBDFF,SBDFF,D5Ba,&
                B5,B5,LX0,&
                B5,B5,LX0,&
                B5,B5,LX0,&
                QD8,BPMQ079,QD8,LX0,&
                B5,B5,LX0,&
                B5,B5,AB7,LX0b,&
                QF7,BPMQ079,QF7,XCORFB,YCORFB,LX0,&
                B5,B5,LX0,&
                B5,B5,LX0,&
                QD6,BPMQ079,QD6,LX0,&
                B5,B5,LX0,&
                B5,B5,LX0,&
                B5,B5,D5A,&
                QF5,BPMQ079,QF5,LX0,&
                SF5,SF5,BPMMB079,LX0,&
                QF5,BPMQ079,QF5,D4,&
                DEC4,D4A,&
                QD4,BPMQ079,QD4,LX0,&
                SD4,SD4,BPMMB079,LX0,&
                QD4,BPMQ079,QD4,D3,D3B,LX0,&
                B2B,B2B,LX0,&
                B2B,B2B,LX0,&
                B2B,B2B,LX0,&
                B2B,B2B,LX0,&
                B2,B2,LX0,XCORFB,&
                B2,B2,LX0,&
                B2,B2,LX0,&
                QD2B,BPMQ079,QD2B,LX0,YCORFB,&
                B2V2,B2V2,LX0,&
                B2,B2,LX0,&
                QF3,BPMQ079,QF3,LX0,&
                B2V1,B2V1,LX0,&
                B2,B2,LX0,&
                QD2A,BPMQ079,QD2A,LX0,&
                B1,B1,LX0,&
                B1,B1,LX0,&
                B1,B1,LX0,&
                B1,B1,LX0,&
                B1,B1,LX0,&
                B1,B1,D2a,MSK1,D2b,CRAB,LX0,ST4,LX0,MSK2,DMSQ,&
                SQ3FF,SQ3FF,LX0,&
                OC1,OC1,LX0,BPMMB079,&
                SF1,SF1,D1CM,&
                OC0,OC0,D1CM,&
                QF1,BPMQ079,QF1,D1B,&
                SD0,SD0,BPMMB079,D1A,&
                QD0,BPMQ079,QD0,XCORIP,YCORIP,D0,IP,&
                ENDEFFL)
  EFFLM : LINE=(-EFFL)
  EFFLS : LINE=(EFFL,IP_PKG)

! ==============================================================================

  RETURN
